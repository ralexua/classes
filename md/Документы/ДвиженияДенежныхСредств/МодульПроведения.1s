Перем тбДолгиПоставщиков,тбДолгиПокупателей;
Перем спФирмы;

// ===============================
Функция ПроверкаШапки()
	глВсеВыбрано = 1;
	глВыбранЛи(Фирма,"Фирма");  
	глВыбранЛи(РСчет,"Расчетный счет");  	
	глВыбранЛи(Валюта,"Валюта");
	Возврат глВсеВыбрано;
КонецФункции
                                                     
// ===============================
Функция ПроверкаСтроки()
    глВсеВыбрано = 1;
	глВыбранЛи(ВидДвижения,"Дв.по р/с",НомерСтроки);
	глВыбранЛи(ВидОперации,"Вид оп.",НомерСтроки);    
	глВыбранЛи(СтавкаНДС,"Ставка НДС",НомерСтроки);    
	Если ВидОперации <> Перечисление.ВидыОплаты.Корректировка Тогда
		глВыбранЛи(Контрагент,"Контрагент",НомерСтроки);    		
	КонецЕсли;	
	Если (Приход = 0) И (Расход = 0) Тогда
		глТрассировка("Не указана сумма в строке "+НомерСтроки+".",0);
		глВсеВыбрано = 0;
	КонецЕсли;	
	Возврат глВсеВыбрано;
КонецФункции                 

// ===============================
Процедура ДвиженияДеньги()

	Для Инд=1 по спФирмы.РазмерСписка() Цикл
		текФирма = спФирмы.ПолучитьЗначение(Инд);
		
		Регистр.Деньги.Фирма = текФирма;
		Регистр.Деньги.Валюта = Валюта;
		
		Если ПустоеЗначение(текФирма)=0 Тогда
			Регистр.Деньги.Счет = РСчет;
		Иначе	
			Регистр.Деньги.Счет = 0;
		КонецЕсли;
		
		Регистр.Деньги.БезНал = 1; // без наличные
		
		ВыбратьСтроки();
		Пока ПолучитьСтроку()=1 Цикл
			Если ВидДвижения = Перечисление.ВидыДвиженийПоРасчетномуСчету.Поступление Тогда
				СуммаСНДС = Приход - Расход;
				Если ВидОперации = Перечисление.ВидыОплаты.Оплата Тогда
					Регистр.Деньги.Сумма = СуммаСНДС;
				Иначе
					Регистр.Деньги.Сумма = - СуммаСНДС;
				КонецЕсли;
				Регистр.Деньги.ДвижениеПриходВыполнить();
			Иначе	
				СуммаСНДС = Расход - Приход;
				Если ВидОперации = Перечисление.ВидыОплаты.Оплата Тогда
					Регистр.Деньги.Сумма = СуммаСНДС;
				Иначе
					Регистр.Деньги.Сумма = - СуммаСНДС;
				КонецЕсли;
				Регистр.Деньги.ДвижениеРасходВыполнить();
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры


// ===============================
Процедура ДвиженияВзаиморасчеты()
	
	спКонтрагенты=СоздатьОбъект("СписокЗначений");
	ВыгрузитьТабличнуюЧасть(спКонтрагенты,"Контрагент");
	
	ВремРегистры=СоздатьОбъект("Регистры");
	
	РегПоставщики=ВремРегистры.ВзаиморасчетыПоставщиков;
	РегПоставщики.УстановитьЗначениеФильтра("Фирма",спФирмы,2);
	РегПоставщики.УстановитьЗначениеФильтра("Контрагент",спКонтрагенты,2);
	
	РегПокупатели=ВремРегистры.ВзаиморасчетыПокупателей;
	РегПокупатели.УстановитьЗначениеФильтра("Фирма",спФирмы,2);	
	РегПокупатели.УстановитьЗначениеФильтра("Контрагент",спКонтрагенты,2);
	
	Если ИтогиАктуальны()=0 Тогда
		РегПоставщики.ВременныйРасчет();
		РегПокупатели.ВременныйРасчет();
		ВремРегистры.РассчитатьРегистрыНа(ТекущийДокумент());
	КонецЕсли;
	
	Для Инд=1 по спФирмы.РазмерСписка() Цикл
		текФирма = спФирмы.ПолучитьЗначение(Инд);
		
		РегПоставщики.УстановитьЗначениеФильтра("Фирма",текФирма,1);
		РегПокупатели.УстановитьЗначениеФильтра("Фирма",текФирма,1);
		
		РегПоставщики.ВыгрузитьИтоги(тбДолгиПоставщиков,1,1);
		РегПокупатели.ВыгрузитьИтоги(тбДолгиПокупателей,1,1);
		
		ВыбратьСтроки();
		Пока ПолучитьСтроку()=1 Цикл
			
			Если (ВидДвижения = Перечисление.ВидыДвиженийПоРасчетномуСчету.Поступление) и 
			(ВидОперации = Перечисление.ВидыОплаты.Оплата) или 
			(ВидДвижения = Перечисление.ВидыДвиженийПоРасчетномуСчету.Списание) и 
			(ВидОперации = Перечисление.ВидыОплаты.Возврат) Тогда
				тбДолги = тбДолгиПокупателей;
				ЗнакОплаты = -1; 
			КонецЕсли;
			Если (ВидДвижения = Перечисление.ВидыДвиженийПоРасчетномуСчету.Поступление) и 
			(ВидОперации = Перечисление.ВидыОплаты.Возврат) или 
			(ВидДвижения = Перечисление.ВидыДвиженийПоРасчетномуСчету.Списание) и 
			(ВидОперации = Перечисление.ВидыОплаты.Оплата) Тогда
				тбДолги = тбДолгиПоставщиков;
				ЗнакОплаты = +1; 
			КонецЕсли;
			
			
			// Пресчет суммы                     
			//*ralex, 09-12-21 22:25
			//Если ПустоеЗначение(текФирма)=1 Тогда
			//	СуммаПогашения = глПересчет(Приход + Расход,Валюта,глКурсДляВалюты(Контрагент.ВалютаВзаиморасчетов,ДатаДок),Контрагент.ВалютаВзаиморасчетов,ДатаДок);
			//	СтавкаНДСПогашения = 0;
			//Иначе    
			//	СуммаПогашения = глПересчет(Приход + Расход,Валюта,ДатаДок,Гривня,ДатаДок);
			//	СтавкаНДСПогашения = СтавкаНДС;
			//КонецЕсли;
			// -------- заменено на:
			Если ПустоеЗначение(текФирма)=1 Тогда
				СуммаПогашения = глПересчет(Приход + Расход,Валюта,ДатаДок,ВалютаВзаиморасчетов,КурсВзаиморасчетов);
				СтавкаНДСПогашения = 0;
			Иначе    
				СуммаПогашения = глПересчет(Приход + Расход,Валюта,ДатаДок,Гривня,ДатаДок);
				СтавкаНДСПогашения = СтавкаНДС;
			КонецЕсли;
			//*/------------------------
			
			глПогашениеДолга(Контекст,ЗнакОплаты,тбДолги,текФирма,Контрагент,ВидОперации,
				ДокументОснование,Договор,РежимОплаты,СуммаПогашения,СтавкаНДСПогашения)
			
		КонецЦикла; // по строкам документа
	КонецЦикла; // по фирмам

КонецПроцедуры

//-------------------------------------
Процедура ОбработкаПроведения()

	Если ДатаДок>РабочаяДата() Тогда
		глНеПроводить(Контекст,"Нельзя проводить документ будущей датой!");
		Возврат;
	КонецЕсли;
	
	Если ПроверкаШапки()=0 Тогда
	    глНеПроводить(Контекст);
		Возврат;
	КонецЕсли;            
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		Если ПроверкаСтроки() = 0 Тогда
			глНеПроводить(Контекст); 
			Возврат;
		КонецЕсли;
	КонецЦикла;		                                 

	спФирмы = глПолучитьФирмы(Контекст);
	
	Если спФирмы.РазмерСписка()>0 Тогда
		                 
		ДвиженияДеньги();
		
		Если ВидОперации <> Перечисление.ВидыОплаты.Корректировка Тогда
			ДвиженияВзаиморасчеты();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры
//----------------------
