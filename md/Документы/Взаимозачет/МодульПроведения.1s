Перем спФирмы;
// ===============================
Функция ПроверкаШапки()
	глВсеВыбрано = 1;
	глВыбранЛи(Покупатель,"Покупатель");
	глВыбранЛи(Поставщик,"Поставщик");
	глВыбранЛи(Валюта,"Валюта");
	Возврат глВсеВыбрано;
КонецФункции

// ===============================
Функция ПроверкаСтроки()
	глВсеВыбрано = 1;
	глВыбранЛи(ДокументКредита,"Документ кредита",НомерСтроки);
	Возврат глВсеВыбрано;
КонецФункции

// ===============================
Процедура ДвиженияВзаиморасчеты()
		
	тбДолгиПоставщиков = 0;
	тбДолгиПокупателей = 0;
	
	// Заполняем списки
	спКонтрагенты = СоздатьОбъект("СписокЗначений");
	спКонтрагенты.ДобавитьЗначение(Поставщик);
	спКонтрагенты.ДобавитьЗначение(Покупатель);
	
	спКредДокументы = СоздатьОбъект("СписокЗначений");
	
	ВыгрузитьТабличнуюЧасть(спКредДокументы,"ДокументКредита");
	
	ВремРегистры=СоздатьОбъект("Регистры");
	
	РегПоставщики=ВремРегистры.ВзаиморасчетыПоставщиков;
	РегПоставщики.УстановитьЗначениеФильтра("Фирма",спФирмы,2);
	РегПоставщики.УстановитьЗначениеФильтра("Контрагент",Поставщик,1);
	РегПоставщики.УстановитьЗначениеФильтра("КредДокумент",спКредДокументы,2);
	
	РегПокупатели=ВремРегистры.ВзаиморасчетыПокупателей;
	РегПокупатели.УстановитьЗначениеФильтра("Фирма",спФирмы,2);	
	РегПокупатели.УстановитьЗначениеФильтра("Контрагент",Покупатель,1);
	РегПокупатели.УстановитьЗначениеФильтра("КредДокумент",спКредДокументы,2);
	
	Если ИтогиАктуальны()=0 Тогда
		РегПоставщики.ВременныйРасчет();
		РегПокупатели.ВременныйРасчет();
		ВремРегистры.РассчитатьРегистрыНа(ТекущийДокумент());
	КонецЕсли;
	
	Для Инд=1 по спФирмы.РазмерСписка() Цикл
		текФирма = спФирмы.ПолучитьЗначение(Инд);
		
		РегПоставщики.УстановитьЗначениеФильтра("Фирма",текФирма,1);
		РегПокупатели.УстановитьЗначениеФильтра("Фирма",текФирма,1);
		
		РегПоставщики.ВыгрузитьИтоги(тбДолгиПоставщиков,1,1);
		РегПокупатели.ВыгрузитьИтоги(тбДолгиПокупателей,1,1);
		
		ВыбратьСтроки();
		Пока ПолучитьСтроку()=1 Цикл
			
			Если ВидКонтрагента = Перечисление.ВидыКлиентов.Покупатель Тогда
				текКонтрагент = Покупатель;
				тбДолги =  тбДолгиПокупателей;
				ЗнакОплаты = -1;
				СуммаПогашения = ДолгПокупателя;
			ИначеЕсли ВидКонтрагента = Перечисление.ВидыКлиентов.Поставщик Тогда	
				текКонтрагент = Поставщик;
				тбДолги =  тбДолгиПоставщиков;
				ЗнакОплаты = +1;
				СуммаПогашения = ДолгПоставщику;
			КонецЕсли;                   
			
			Если ПустоеЗначение(текФирма)=1 Тогда
				ВУ = текКонтрагент.ВалютаВзаиморасчетов;
			Иначе
				ВУ = Гривня;
			КонецЕсли;
			
			СуммаПогашения = глПересчет(СуммаПогашения,Валюта,ДатаДок,ВУ,ДатаДок);
			
			ФлагОтгрузки = 0;
			ФлагВозврата = 0;
			
			тбДолги.Сортировать("Контрагент,КредДокумент",1);
			тбДолги.ВыбратьСтроки();
			Пока тбДолги.ПолучитьСтроку()=1 Цикл
				Если глПроверитьДолг(тбДолги,,,,ДокументКредита)=0 Тогда
					продолжить;
				КонецЕсли;
				СальдоПоОтгрузке=?(ЗнакОплаты<0,Макс(0, тбДолги.Долг), -Мин(0, тбДолги.Долг));
				Если СальдоПоОтгрузке <> 0 Тогда
					СуммаСНДС = Мин(СуммаПогашения, СальдоПоОтгрузке);
					тбДолги.Долг = тбДолги.Долг + ЗнакОплаты * СуммаСНДС;
					СуммаПогашения = СуммаПогашения - СуммаСНДС;
					ФлагНУ = 0;                                 
					КодОперации = ПостОплата;
					глПогаситьДокументВзаиморасчетов(Контекст,ЗнакОплаты, ФлагОтгрузки, ФлагВозврата, 
					тбДолги.Фирма, тбДолги.Контрагент, тбДолги.Договор, 
					тбДолги.СтавкаНДС, тбДолги.КредДокумент, СуммаСНДС, КодОперации, ФлагНУ);
				КонецЕсли;
				
				Если СуммаПогашения = 0 Тогда
					прервать;
				КонецЕсли;					
			КонецЦикла;
			
			Если СуммаПогашения > 0 Тогда
			    глНеПроводить(Контекст,"Не распределился долг по взаиморасчетам."+РазделительСтрок+
							Шаблон("Остаток долга [СуммаПогашения] [ВУ.Кратко]. Строка [НомерСтроки]"));
				Возврат;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

//-------------------------------------
Процедура ОбработкаПроведения()
	
	Если ДатаДок>РабочаяДата() Тогда
		глНеПроводить(Контекст,"Нельзя проводить документ будущей датой!");
		Возврат;
	КонецЕсли;
	
	Если ПроверкаШапки() = 0 Тогда
		глНеПроводить(Контекст); 
		Возврат;
	КонецЕсли;                      
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		Если ПроверкаСтроки() = 0 Тогда
			глНеПроводить(Контекст); 
			Возврат;
		КонецЕсли;
	КонецЦикла;		                                 
	
	спФирмы = глПолучитьФирмы(Контекст);
	
	Если спФирмы.РазмерСписка()>0 Тогда
		
		ДвиженияВзаиморасчеты();
		
	КонецЕсли;
	
КонецПроцедуры


