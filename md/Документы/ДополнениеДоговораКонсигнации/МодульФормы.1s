
Перем НачальнаяДатаДокумента;
//*****************************************************************************
// "служебные" функции и процедуры 

//*****************************************************************************
Функция ЗаголовокФормы() 
	Перем Заголовок, Название;
	Заголовок = "Дополнение к договору";
	Название ="Дополнение №";
	
	Заголовок = Заголовок + глЗаголовок(Контекст);
	
	Форма.Заголовок(Заголовок);     
	Возврат Название;
КонецФункции //ЗаголовокФормы

// ===============================
Процедура УстДоступность()
	Если Выбран()=0 Тогда
	    Возврат;
	КонецЕсли;
	// Проверяем были ли движения по взаиморасчетам с данным договором
	
	фЕстьДвижения=0;
	
	РегВзаим=СоздатьОбъект("Регистр.ВзаиморасчетыПоставщиков");
	РегВзаим.УстановитьЗначениеФильтра("Договор",ТекущийДокумент(),1);
	РегВзаим.ВыбратьДвижения('01.01.2000',,);
	Если РегВзаим.ПолучитьДвижение()=1 Тогда
		фЕстьДвижения=1;
	КонецЕсли;
	РегВзаим=0;
	Если фЕстьДвижения=0 Тогда
		РегВзаим=СоздатьОбъект("Регистр.ВзаиморасчетыПокупателей");
		РегВзаим.УстановитьЗначениеФильтра("Договор",ТекущийДокумент(),1);
		РегВзаим.ВыбратьДвижения('01.01.2000',,);
		Если РегВзаим.ПолучитьДвижение()=1 Тогда
			фЕстьДвижения=1;
		КонецЕсли;
		РегВзаим=0;
	КонецЕсли;
	Если фЕстьДвижения=1 Тогда
		Форма.СпособФормированияНалоговыхДокументов.Доступность(0);
		Форма.СпособФормированияНалоговыхДокументов2.Доступность(0);
		Форма.рСпособ.Доступность(0);
	КонецЕсли;
	
КонецПроцедуры

//******************************************************************************
Процедура ВводНаОсновании(ДокОснование)
	
	Если ДокОснование.Вид()<>"Договор" Тогда
		Предупреждение("Дополнение договора можно вводить только на основании договора");
		СтатусВозврата(0);
	    Возврат;
	КонецЕсли;
	             
	Фирма = ДокОснование.Фирма;

	Договор = ДокОснование;
	                  
	Если Договор.ВидТорговли <> Перечисление.ВидыТорговли.Консигнация Тогда
		Предупреждение("Дополнение договора консигнации можно вводить только на основании договора консигнации");
		СтатусВозврата(0);
	    Возврат;
	КонецЕсли;
	
	глЗаполнитьШапку(Контекст);
	
	Контрагент = ДокОснование.Контрагент;
	
	ВидДополнения = Перечисление.ВидыДополненийДоговоров.Консигнация;
	
	СпособФормированияНалоговыхДокументов = ДокОснование.СпособФормированияНалоговыхДокументов;
	
КонецПроцедуры                               
                         
//*****************************************************************************
Процедура ВыборОснования()
	// Процедура по кнопке редактирования основания в докумнете
	Перем КонтекстДокумента;
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ОснованиеДокумента", КонтекстДокумента);
	// Могли изменить Контрагента
	 ВводНаОсновании(Договор);
КонецПроцедуры	

//*****************************************************************************
Процедура ВыборФирмы()
	// по кнопке редактирования параметров фирмы в докумнете
	Перем КонтекстДокумента;
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ИнформацияОфирме", КонтекстДокумента);
КонецПроцедуры	

//*****************************************************************************
Процедура ПриНачалеВыбораЗначения(Рекв,Флаг)	
	Если Рекв="Фирма" Тогда
		ВыборФирмы();
		Флаг=0;
	ИначеЕсли Рекв="Договор" Тогда
		ВыборОснования();
		Флаг=0;
	КонецЕсли;
КонецПроцедуры

//******************************************************************************
Процедура ВводНового(Скопирован) //Предопределенная процедура

	глЗаполнитьШапку(Контекст);
	Если Скопирован=1 Тогда	//копирование документа
		Возврат;
	КонецЕсли;

	ДатаДок=РабочаяДата();
	ДатаНачала=ДатаДок;
	
	Контрагент=Константа.ОсновнойПокупатель;
	
	СпособФормированияНалоговыхДокументов=1;
	
	Если ПустоеЗначение(ВидДополнения)=1 Тогда
		ВидДополнения = Перечисление.ВидыДополненийДоговоров.Консигнация;
	КонецЕсли;
	
КонецПроцедуры

//*****************************************************************************
Процедура ПриОткрытии() //Предопределенная процедура
	НачальнаяДатаДокумента = ДатаДок;
	глПроверкаРазрешенияРедактирования(Контекст);

	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кнОК.Доступность(0);
		Форма.кнПровести.Доступность(0);
		Форма.кнДействия.Доступность(0);
	Иначе
		Форма.КнопкаПоУмолчанию("кнОК");
	КонецЕсли;

	УстДоступность();
КонецПроцедуры //При открытии

//*****************************************************************************
Процедура ПриЗаписи() //Предопределенная процедура
	Если глМожноЗаписатьДокумент(Контекст)=0 Тогда
		СтатусВозврата(0);                        
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры

//*****************************************************************************
//Инициализирум список действий по кнопке "Действия"           
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ОткрытьВЖурнале,
	|Подчиненные,
	|ИзмКомментария");
