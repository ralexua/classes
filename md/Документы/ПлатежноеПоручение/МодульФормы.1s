//*****************************************************************************
// Описание переменных

Перем СтараяФирма, СтарыйКонтрагент;
Перем СписокДействий;
Перем СтараяДата;

Перем НачальнаяДатаДокумента;

//*****************************************************************************
// "служебные" функции и процедуры 

//*****************************************************************************
Функция ЗаголовокФормы() 
	Перем Заголовок, Название;
	Заголовок = "Платежное поручение";
	Название = "Платежное поручение № ";
	
	Заголовок = Заголовок + глЗаголовок(Контекст);
	
	Форма.Заголовок(Заголовок);               
	Возврат Название;
КонецФункции //ЗаголовокФормы

//******************************************************************************
Функция ФормирСуммаНДС(Подст)
	Перем Номер;
	
	СтрПодст="";
    СтрСумма="";
	СтрНДС="";
	СтрКон="";
	Номер=Подст.ПорядковыйНомер();
	Если (Номер=1) ИЛИ (Номер=3) Тогда
	   	СтрСумма="Сума "+СокрЛ(Формат(СуммаСНДС,"Ч15.2,'"))+" грн";
	КонецЕсли;
	Если (Номер=1) ИЛИ (Номер=2) Тогда
		Если НДС=0 Тогда
			СтрНДС=", без ПДВ";
		Иначе
			СтрНДС=", у т.ч. ПДВ("+СтавкаНДС+") "+СокрЛ(Формат(НДС,"Ч15.2,'"))+" грн";
		КонецЕсли;
	КонецЕсли;
	СтрПодст=СтрСумма+СтрНДС;
	Если ПустаяСтрока(СтрПодст)=0 Тогда
		СтрПодст=СтрПодст+"."
	КонецЕсли;
	Возврат СтрПодст;
КонецФункции

//******************************************************************************
Процедура ИзмКонтрагент() // Процедура при выборе Контрагента в докумнете
	Перем Счета;
	Если Контрагент.Выбран() = 1 Тогда
		Если СтарыйКонтрагент <> Контрагент Тогда
			// очистим договор
			Договор = ПолучитьПустоеЗначение("Документ.Договор");
			Если Константа.ПодставлятьОсновнойДоговор = Да Тогда
				// подставим договор по умолчанию                                 
				Если ПустоеЗначение(Контрагент.ОсновнойДоговорТорг) = 0 Тогда
					Если Фирма = Контрагент.ОсновнойДоговорТорг.Фирма Тогда
						Договор = Контрагент.ОсновнойДоговорТорг;
					КонецЕсли;	
				КонецЕсли;	
			КонецЕсли;
			
			СчетКонтрагента= ПустоеЗначение("Справочник.РасчетныеСчета");
			
			Счета = СоздатьОбъект("Справочник.РасчетныеСчета");
			Счета.ИспользоватьВладельца(Контрагент);
			Счета.ВыбратьЭлементы();
			Пока Счета.ПолучитьЭлемент() <> 0 Цикл
				Если Счета.ПометкаУдаления() = 1 Тогда
					Продолжить;
				КонецЕсли;   				
				СчетКонтрагента = Счета.ТекущийЭлемент();
			КонецЦикла;
		КонецЕсли;
	Иначе
		Договор = ПолучитьПустоеЗначение("Документ.Договор");
		СчетКонтрагента = ПолучитьПустоеЗначение("Справочник.РасчетныеСчета");
	КонецЕсли;
	СтарыйКонтрагент = Контрагент;
КонецПроцедуры	
	                            
// ===============================
Процедура ИзмДатаДок()
	глПриИзмененииДатыДокумента(Контекст, СтараяДата);	
КонецПроцедуры

//******************************************************************************
Процедура ПриВводеНаОсновании(ДокОснование)    
	
	Если ПустоеЗначение(ДокОснование)=1 Тогда
		Возврат;
	КонецЕсли;
	
	Содержание="За "+глУкр(ДокОснование.ПредставлениеВида())+" № " + ДокОснование.НомерДок + " від " + ДокОснование.ДатаДок;

	глЗаполнитьШапкуНаОсн(Контекст,ДокОснование);
	
	Если ПустоеЗначение(СуммаСНДС) = 1 Тогда
		
		// спросим у пользователя, переоформить или нет
		
		Если глПереоформитьДокумент(Контекст, ДокОснование) = 0 Тогда
		    Возврат;
		КонецЕсли;              
		
		// можно заполнять
		Если ТипУчета = Упр Тогда
			ВалУч = Контрагент.ВалютаВзаиморасчетов;
		Иначе
			ВалУч = Гривня;
		КонецЕсли;
		
		СуммаСНДС = глПересчет(глДолгКонтрагента(Контекст ,+1, ТипУчета, Фирма, Контрагент, Договор ,ДокументОснование),ВалУч,ДатаДок,Валюта,ДатаДок);
		
		Если глЕстьРеквизитМнЧ("СуммаСНДС",ДокОснование.Вид()) = Да Тогда
			Если СуммаСНДС = 0 Тогда                                   
				Вл = ?(глЕстьРеквизитШапки("Валюта",ДокОснование.Вид()) = Да,ДокОснование.Валюта,ВалУч);
				СуммаСНДС = глПересчет(ДокОснование.Итог("СуммаСНДС"),Вл,ДатаДок,Валюта,ДатаДок);
			КонецЕсли;	
		КонецЕсли;
		
		НДС = СуммаСНДС*глПроцентНДС(СтавкаНДС)/100;
			
	КонецЕсли;
КонецПроцедуры

//*****************************************************************************
// функции и процедуры, вызываемые из формул элементов диалога

//******************************************************************************

//******************************************************************************
Процедура Печать()
	
	Если РСчет.Валюта <> Гривня Тогда
	    Предупреждение("Предусмотрена печать только гривневых платежек!");
	    Возврат;
	КонецЕсли;
	Фирма.ИспользоватьДату(ДатаДок);
	Таб=СоздатьОбъект("Таблица");
	Пропись("Uahukr.spl"); // строго на украинском!
	Эк = 2;
	СуммаСНДС_стр = Сред(Формат(Цел(СуммаСНДС), "ЧПД"),1,Найти(Формат(Цел(СуммаСНДС), "ЧПД"),
	                "гр") - 2) + " грн. " + Формат((СуммаСНДС - Цел(СуммаСНДС))*100,"Ч(0)2") 
					+ " коп.";
	ВвестиЧисло(Эк,"Введите количество экземпляров",1,0);
    Для Ном = 1 По Эк Цикл
		Таб.ВывестиСекцию("Форма");
		Если Ном/2 = Окр(Ном/2) Тогда
		    Таб.НоваяСтраница();
		КонецЕсли;
	КонецЦикла;
	
	Таб.Опции(0,0,0,0,"");
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Платежное поручение","");
	глУстПропись(Гривня); // восстановим пропись на нужном языке
	
КонецПроцедуры

//*****************************************************************************
Процедура ВыборОснования()
	// Процедура по кнопке редактирования основания в докумнете
	Перем КонтекстДокумента;
	СтарДоговор = Договор;
	СтарОснование = ДокументОснование;
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ОснованиеДокумента", КонтекстДокумента);
	Если ((ДокументОснование <> СтарОснование) и (ПустоеЗначение(ДокументОснование) = 0)) тогда
		ПриВводеНаОсновании(ДокументОснование);	
	ИначеЕсли Договор<>СтарДоговор тогда
		ПриВводеНаОсновании(Договор);			
	КонецЕсли;	
	Форма.ТекстОснования.Заголовок(СокрП(Основание));
КонецПроцедуры	

//******************************************************************************
Процедура ИзмАвтоПодстСуммыНДС()
	Если АвтоПодстСуммыНДС.Выбран() = 1 Тогда
	    СохранитьЗначение("ПлатежноеПоручениеАвтоПодстСуммыНДС",АвтоПодстСуммыНДС);
	Иначе
		АвтоПодстСуммыНДС =	ВосстановитьЗначение("ПлатежноеПоручениеАвтоПодстСуммыНДС");
	КонецЕсли;
КонецПроцедуры

//******************************************************************************
Процедура РасчетНДС()
	ПроцНДС = глПроцентНДС(СтавкаНДС);
	НДС = СуммаСНДС*ПроцНДС/(100+ПроцНДС);
КонецПроцедуры

//*****************************************************************************
Процедура ВыборФирмы()
	// по кнопке редактирования параметров фирмы в докумнете
	Перем КонтекстДокумента;
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ИнформацияОфирме", КонтекстДокумента);
КонецПроцедуры	
                                                                     
// ===============================
Процедура УстНомерПлатежки()     
	Если ((РСчет.Выбран()=1) и (ПустоеЗначение(Номер) = 1)) Тогда
		Номер = РСчет.ПоследнийРасхДок + 1;
	КонецЕсли;	
КонецПроцедуры

//*****************************************************************************
Процедура УстРСчет(ДокОснование = "")
	РСчет = ПолучитьПустоеЗначение("Справочник.НашиДенежныеСчета");
	// Найдем расчетный счет по валюте документа-основания
	РСчета = СоздатьОбъект("Справочник.НашиДенежныеСчета");
	Если ПустоеЗначение(ДокОснование) = 0 Тогда
		Если РСчета.ВыбратьЭлементыПоРеквизиту("Валюта",ДокОснование.Валюта,1,0) = 1 Тогда
			Пока РСчета.ПолучитьЭлементы() = 1 Цикл
				Если РСчета.БезНал = 1 Тогда
					РСчет = РСчета.ТекущийЭлемент();				
					Прервать;
				КонецЕсли;
			КонецЦикла;	
		КонецЕсли;
	ИначеЕсли РСчета.ВыбратьЭлементыПоРеквизиту("Валюта",Валюта,1,0) = 1 Тогда
		Пока РСчета.ПолучитьЭлемент() = 1 цикл
			Если РСчета.БезНал = 1 тогда
				РСчет = РСчета.ТекущийЭлемент(); 
				Прервать;
			КонецЕсли;	
		КонецЦикла;	          	                                                  
	Иначе
		РСчет = Фирма.СчетПоУмолчанию;
	КонецЕсли;				          
	УстНомерПлатежки();
КонецПроцедуры	

//*****************************************************************************
// Предопределенные процедуры

//******************************************************************************
 Процедура ПриНачалеВыбораЗначения(Элемент,Флаг) // Предопределенная

	Если (Элемент = "Фирма") или (Элемент = "РСчет") Тогда
		Флаг = 0;
		ВыборФирмы();                             
		Валюта = РСчет.Валюта; 
	КонецЕсли;
	
КонецПроцедуры //ПриНачалеВыбораЗначения

//******************************************************************************
Процедура ВводНового(Скопирован)

	глЗаполнитьШапку(Контекст);
	Если Скопирован=1 Тогда	//копирование документа
		Возврат;
	КонецЕсли;

	Если ПустоеЗначение(глАктивныйДоговор) = 0 Тогда
		Контрагент = глАктивныйДоговор.Владелец;
		Договор = глАктивныйДоговор;
		СтарыйКонтрагент = Контрагент;
	Иначе
		Контрагент=Константа.ОсновнойПокупатель;
		ИзмКонтрагент();
	КонецЕсли;

	АвтоПодстСуммыНДС =	ВосстановитьЗначение("ПлатежноеПоручениеАвтоПодстСуммыНДС");

	Если АвтоПодстСуммыНДС.Выбран() = 0 Тогда
	    АвтоПодстСуммыНДС = Перечисление.ПодстановкаСуммыНДС.СуммаНДС;
	    СохранитьЗначение("ПлатежноеПоручениеАвтоПодстСуммыНДС",АвтоПодстСуммыНДС);
	КонецЕсли;
	СтавкаНДС=Константа.ОсновнаяСтавкаНДС;
	
	Валюта=Константа.БазоваяВалюта;
	УстРСчет();
КонецПроцедуры

//******************************************************************************
Процедура ВводНаОсновании(ДокОснование)
	Автор = глПользователь;
	ДокВид=ДокОснование.Вид();
	Если Найти("Договор/ПриходнаяНакладная/СчетВходящий/ГТД", ДокВид) > 0 Тогда
		
		Валюта=Гривня;
		ДатаДок=РабочаяДата();
		
		АвтоПодстСуммыНДС =	ВосстановитьЗначение("ПлатежноеПоручениеАвтоПодстСуммыНДС");
		Если АвтоПодстСуммыНДС.Выбран() = 0 Тогда
		    АвтоПодстСуммыНДС = Перечисление.ПодстановкаСуммыНДС.СуммаНДС;
		    СохранитьЗначение("ПлатежноеПоручениеАвтоПодстСуммыНДС",АвтоПодстСуммыНДС);
		КонецЕсли;
		СтавкаНДС=Константа.ОсновнаяСтавкаНДС;
		
		СтарыйКонтрагент = Контрагент;
		ИзмКонтрагент();

		Если СтараяФирма <> Фирма Тогда
			УстРСчет(ДокОснование);			
		КонецЕсли;
		СтараяФирма = Фирма;
		
		// для другой фирмы может быть другой префикс номера
		глУстановитьНомерДок(Контекст);
		
		ПриВводеНаОсновании(ДокОснование);		
	Иначе
		Предупреждение("Платежное поручение нельзя вводить на основании выбранного вида документа!");
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры

//******************************************************************************
Процедура ПриОткрытии()
	НачальнаяДатаДокумента = ДатаДок;
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кнОК.Доступность(0);
		Форма.кнДействия.Доступность(0);
		
		Форма.кнОчиститьНДС.Доступность(0);
		
		Форма.кнОснование.Доступность(0);
	Иначе
		Форма.КнопкаПоУмолчанию("кнОК");
	КонецЕсли;
	// основание
	Форма.ТекстОснования.Заголовок(СокрП(Основание));
	СтараяДата = ДатаДок;
КонецПроцедуры

//******************************************************************************
Процедура ПриЗаписи()
	Если глМожноЗаписатьДокумент(Контекст)=0 Тогда
		СтатусВозврата(0);                        
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;                                      
	Если РСчет.ПоследнийРасхДок < Номер Тогда
		Сч = СоздатьОбъект("Справочник.НашиДенежныеСчета");
		Сч.ИспользоватьВладельца(Фирма);
		Сч.НайтиЭлемент(РСчет);
		Сч.ПоследнийРасхДок = Номер;
		Сч.Записать();
	КонецЕсли;
КонецПроцедуры

//Инициализирум список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ОткрытьВЖурнале,
	|ИзмКомментария");