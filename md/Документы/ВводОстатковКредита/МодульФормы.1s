Перем СтарыйКонтрагент;
Перем Рег;
Перем ВалютаКредита;
Перем СтарыйДоговор;
Перем СтарыйКлиент;
Перем СписокДействий;  // Список действий по документу
Перем СтараяДата;
Перем НачальнаяДатаДокумента; // Для контроля даты документа

// ===============================
// "служебные" функции и процедуры 


// ===============================
Функция ЗаголовокФормы() 
	Перем Заголовок, Название;

	Заголовок = "Ввод остатков по кредиту";
	Название = "Остатки кредита №";

	Заголовок = Заголовок + глЗаголовок(Контекст);
	
    Форма.Заголовок(Заголовок);     
	Возврат Название;
КонецФункции //ЗаголовокФормы

// ===============================
Процедура ОбновитьНадписи()
	// Формируем информационные строки
	Форма.ТекстВалюты.Заголовок(глСтрокаВалюты(Контекст));
	Форма.ТекстПорядокОплаты.Заголовок(глСтрокаПорядокОплаты(Контекст)); 
	Форма.ТекстОснования.Заголовок(СокрП(Основание));
КонецПроцедуры


// ===============================
Процедура УстДоступностьГлубина()
	Если ВидКонтрагента=Перечисление.ВидыКлиентов.Покупатель Тогда
		Если НЕ(Контрагент.СуммаКредита.Получить(ДатаДок)=0) Тогда
			Форма.ДатаОплПодпись.Видимость(1);
			Форма.ДатаОпл.Видимость(1);			
			Форма.ГлубинаПодпись.Видимость(1);
			Форма.Глубина.Видимость(1);			
		Иначе                                                
			Форма.ДатаОплПодпись.Видимость(0);
			Форма.ДатаОпл.Видимость(0);			
			Форма.ГлубинаПодпись.Видимость(0);
			Форма.Глубина.Видимость(0);			
		КонецЕсли;
	ИначеЕсли ВидКонтрагента=Перечисление.ВидыКлиентов.Поставщик Тогда
		Если НЕ(Контрагент.СуммаКредитаПоставщика.Получить(ДатаДок)=0) Тогда
			Форма.ДатаОплПодпись.Видимость(1);
			Форма.ДатаОпл.Видимость(1);			
			Форма.ГлубинаПодпись.Видимость(1);
			Форма.Глубина.Видимость(1);			
		Иначе
			Форма.ДатаОплПодпись.Видимость(0);
			Форма.ДатаОпл.Видимость(0);			
			Форма.ГлубинаПодпись.Видимость(0);
			Форма.Глубина.Видимость(0);			
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;
КонецПроцедуры	

// ===============================
Процедура УстДоступностьКнопок()
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кнОК.Доступность(0);
		Форма.кнПровести.Доступность(0);
		Форма.кнДействия.Доступность(0);
		
		Форма.кнЗаполнить.Доступность(0);
		
		Форма.кнОснование.Доступность(0);
		Форма.кнВалюта.Доступность(0);
	Иначе
		Форма.КнопкаПоУмолчанию("кнОК");
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ИзмФлагОтгрузки()
    ФлагОтгрузки=2 - ФлагиОтгрузки.ТекущаяСтрока();
КонецПроцедуры

// ===============================
Процедура Взаиморасчеты()
	Если ВидКонтрагента = Перечисление.ВидыКлиентов.Покупатель Тогда
		глПоказатьДолг(Контекст, Контрагент, "Продажа");
	ИначеЕсли ВидКонтрагента = Перечисление.ВидыКлиентов.Поставщик Тогда
		глПоказатьДолг(Контекст, Контрагент, "Закупка");
	Иначе
		Предупреждение("Не выбран вид клиента.");
		Активизировать("ВидКонтрагента");
	КонецЕсли;
КонецПроцедуры     

// ===============================
Процедура ИзмГлубина()
	Если ПустоеЗначение(Глубина) = 1 Тогда              
		Глубина = 0;
	КонецЕсли;	
	ДатаОплаты=глБанковскаяДата(ДатаДок,Глубина);	
КонецПроцедуры

// ===============================
Процедура УстГлубина()
	Если ВидКонтрагента=Перечисление.ВидыКлиентов.Покупатель Тогда
		Если НЕ(Контрагент.СуммаКредита.Получить(ДатаДок)=0) Тогда
			ВалютаКредита=Контрагент.ВалютаКредита;
			Глубина=Контрагент.Глубина.Получить(ДатаДок);
		Иначе                                                
			ВалютаКредита=Контрагент.ВалютаВзаиморасчетов;
			Глубина=0;
		КонецЕсли;
	ИначеЕсли ВидКонтрагента=Перечисление.ВидыКлиентов.Поставщик Тогда
		Если НЕ(Контрагент.СуммаКредитаПоставщика.Получить(ДатаДок)=0) Тогда
			ВалютаКредита=Контрагент.ВалютаКредитаПоставщика;
			Если Глубина <> Контрагент.ГлубинаКредитаПоставщика.Получить(ДатаДок) тогда
				Глубина=Контрагент.ГлубинаКредитаПоставщика.Получить(ДатаДок);
			КонецЕсли;	
		Иначе
			ВалютаКредита=Контрагент.ВалютаВзаиморасчетов;
			Глубина=0;               
		КонецЕсли;
	КонецЕсли;
	ИзмГлубина();
КонецПроцедуры

// ===============================
Процедура ИзмДата()
	глПриИзмененииДатыДокумента(Контекст, СтараяДата);
	УстГлубина();
	УстДоступностьГлубина();
	ОбновитьНадписи();
КонецПроцедуры

// ===============================
Процедура ВыборФирмы()
	// по кнопке редактирования параметров фирмы в докумнете
	Перем КонтекстДокумента;
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ИнформацияОфирме", КонтекстДокумента);
	ОбновитьНадписи();
КонецПроцедуры	
	
// ===============================
Процедура ВыборОплаты()
	// Процедура по кнопке редактирования параметров оплаты в докумнете
	Перем КонтекстДокумента;
	Перем Врем;
	// Теперь запомним промежуточные переменные
	Валюта_Прежн=Валюта;
	Курс_Прежн=Курс;
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ИнформацияОценах", КонтекстДокумента);
	СуммаСНДС=глПересчет(СуммаСНДС,Валюта_Прежн,Курс_Прежн,Валюта,Курс);
	ОбновитьНадписи();
КонецПроцедуры	

// ===============================
Процедура ИзмКонтрагент()
    Если Контрагент.Выбран()=1 Тогда                                      
		Если СтарыйКонтрагент <> Контрагент Тогда
			// изменили Контрагента
			// очистим договор
			Договор = ПолучитьПустоеЗначение("Документ.Договор");
			Если Константа.ПодставлятьОсновнойДоговор = Да Тогда
				// подставим договор по умолчанию                                 
				Если ПустоеЗначение(Контрагент.ОсновнойДоговорТорг) = 0 Тогда
					Если Фирма = Контрагент.ОсновнойДоговорТорг.Фирма Тогда
						Договор = Контрагент.ОсновнойДоговорТорг;
					КонецЕсли;	
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;
	Иначе
		//Не выбран Контрагент - не имеет смысла показывать глубину и дату оплаты и договор
		Договор = ПолучитьПустоеЗначение("Документ.Договор");
	КонецЕсли;
	// Формирует и показывает дату оплаты
	СтарыйКонтрагент = Контрагент;
	ОбновитьНадписи();
КонецПроцедуры

// ===============================*
Процедура ИзмВидКонтрагента()
	Если ПустоеЗначение(ВидКонтрагента) = 1 Тогда
		// не задан вид клиента. ничего не делаем
		Возврат;
	Иначе
		Если ВидКонтрагента <> Перечисление.ВидыКлиентов.Поставщик Тогда
		    // если Контрагент не поставщик, то кредитный документ не нужен
			КредДокумент = 0;
		КонецЕсли;
	КонецЕсли;
	
	Если ПустоеЗначение(ВалютаКредита) = 1 Тогда
		ВалютаКредита=Контрагент.ВалютаВзаиморасчетов;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ВыборОснования()
	// Процедура по кнопке редактирования основания в докумнете
	Перем КонтекстДокумента;
	Перем СтрокаЗапятая;
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ОснованиеДокумента", КонтекстДокумента);
	ОбновитьНадписи();
КонецПроцедуры	     

// ===============================
Процедура ПриНачалеВыбораЗначения(Рекв,Флаг)
	Флаг=0;
	Если Рекв="Фирма" Тогда
		ВыборФирмы();
	Иначе
		Флаг=1;
	КонецЕсли;
КонецПроцедуры


// ===============================
Процедура УстанОстатка();
	Если Контрагент.Выбран()=1 Тогда
		ВремРегистры=СоздатьОбъект("Регистры");
		Если ВидКонтрагента=Перечисление.ВидыКлиентов.Покупатель Тогда
			ИмяРегистрУчета="ВзаиморасчетыПокупателей";
			Рег=ВремРегистры.ВзаиморасчетыПокупателей;
		ИначеЕсли ВидКонтрагента=Перечисление.ВидыКлиентов.Поставщик Тогда
			ИмяРегистрУчета="ВзаиморасчетыПоставщиков";
			Рег=ВремРегистры.ВзаиморасчетыПоставщиков;
		Иначе
			Предупреждение ("Не выбран вид клиента!");
			Возврат;
		КонецЕсли;
		
		ВалютаКредита = глВалютаВзаиморасчетов(Контрагент);
		
		Если ТипУчета>Упр Тогда
			ПромФирма=Фирма;
		Иначе
			ПромФирма="";
		КонецЕсли;
		
		Если Выбран()>0 Тогда // документ не новый, а существующий
			Если (СравнитьТА()<0)  Тогда
		 	// если итоги не актуальны, то Долги по Кредиту берем из временногно расчета Регистра
				Рег.ВременныйРасчет();
				Рег.УстановитьФильтр(ПромФирма,Контрагент,Договор,,);
				ВремРегистры.РассчитатьРегистрыПо(ТекущийДокумент(),"Контрагент");
			// здесь показываем Долги по Кредиту именно на момент (после) проводки Документа
			КонецЕсли;
		Иначе // документ новый
			Дат=ПолучитьДатуТА();
		 	Если Дат>ДатаДок Тогда
		 	// если итоги не актуальны, то остатки квоты берем из временногно расчета Регистра
				Рег.ВременныйРасчет();
				Рег.УстановитьФильтр(ПромФирма,Контрагент,Договор,,);
				ВремРегистры.РассчитатьРегистрыНа(ДатаДок+1,"Контрагент");
			КонецЕсли;
		КонецЕсли;
		Если ТипУчета>Упр Тогда
			ВалютаКредита=Гривня;
			УстСтавкаНДС=СтавкаНДС;
		Иначе
			УстСтавкаНДС="";
		КонецЕсли;
		СуммаСНДСОст=Рег.СводныйОстаток(ПромФирма,Контрагент,Договор,УстСтавкаНДС,,"Долг");
		СуммаСНДС=глПересчет(СуммаСНДСОст,ВалютаКредита,ДатаДок,Валюта,Курс);
	Иначе
		Предупреждение ("Не выбран Контрагент!");
		Возврат;
	КонецЕсли;
	Рег=0;
КонецПроцедуры

// ===============================
Процедура ВводНового(Скопирован)

	глЗаполнитьШапку(Контекст);
	Если Скопирован=1 Тогда	//копирование документа
		Возврат;
	КонецЕсли;

	ДатаДок=РабочаяДата();
	Валюта=Гривня;
	Основание="Начальный ввод остатков по торговому кредиту";
	Дата_Курса=ДатаДок;
	Курс=глКурсДляВалюты(Валюта,Дата_Курса);
	
	РежимОплаты = Перечисление.РежимыОплаты.КонкретныйДоговор;

	ВидКонтрагента=Перечисление.ВидыКлиентов.Покупатель;
	
	СтавкаНДС = Константа.ОсновнаяСтавкаНДС;
	
	Если ПустоеЗначение(глАктивныйДоговор) = 0 Тогда
		Контрагент = глАктивныйДоговор.Владелец;
		Договор = глАктивныйДоговор;
		СтарыйКлиент = Контрагент;
	Иначе
		Контрагент=Константа.ОсновнойПокупатель;
		УстДоступностьГлубина();			
	КонецЕсли;

	Если ФлагОтгрузки = 0 Тогда
		ФлагОтгрузки = 1;
	КонецЕсли;
	ФлагиОтгрузки.ТекущаяСтрока(2 - ФлагОтгрузки); 
	                              
	СтарыйКонтрагент = Контрагент;

	УстГлубина();	
 	УстДоступностьГлубина();	
КонецПроцедуры

// ===============================
Процедура ВводНаОсновании(ДокОснование)
	Предупреждение("Документ """+ПредставлениеВида()+""" не вводят на основании других документов!");
	СтатусВозврата(0);
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()
	Перем Позиция;
	
	НачальнаяДатаДокумента = ДатаДок;
	
	глПроверкаРазрешенияРедактирования(Контекст);
	
	глУстановкаРеквизитаТип(Контекст);
	
	УстДоступностьКнопок();
	
	ОбновитьНадписи(); 

	УстДоступностьГлубина();	
	
	//ИзмКонтрагент();
	ФлагиОтгрузки.ТекущаяСтрока(2 - ФлагОтгрузки); 
	                                             
	СтараяДата = ДатаДок;
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	Если глМожноЗаписатьДокумент(Контекст)=0 Тогда
		СтатусВозврата(0);                        
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры

// При входе в Форму запомним промежуточные переменные
ФлагиОтгрузки.ДобавитьЗначение(1,"Отгрузка товара");
ФлагиОтгрузки.ДобавитьЗначение(0,"Оплата");

//Инициализирум список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ДвиженияДокумента,
	|ВводНаОсновании,
	|ОткрытьВЖурнале,
	|ИзмКомментария");
