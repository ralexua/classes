Перем НачальнаяДатаДокумента;
Перем СтараяДата;

// ===============================
Процедура ВводНового()
	Предупреждение("Этот документ можно вводить только на основании Налоговой накладной!");
	СтатусВозврата(0);
КонецПроцедуры

//*****************************************************************************
Функция ЗаголовокФормы() 
	Перем Заголовок, Название;
	Заголовок = "Приложение № 2 к налоговой накладной ";
	Название ="Расчет корректировки №";
	
	Заголовок = Заголовок + глЗаголовок(Контекст);
	
	Форма.Заголовок(Заголовок);     
	Возврат Название;
КонецФункции //ЗаголовокФормы

//******************************************************************************
Процедура ОбновитьНадписи()
	Форма.ТекстОснования.Заголовок(СокрП(Основание));
КонецПроцедуры

//*****************************************************************************
Процедура ВыборОснования()
	// Процедура по кнопке редактирования основания в докумнете
	Перем КонтекстДокумента;
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ОснованиеДокумента", КонтекстДокумента);
	// Могли изменить Контрагента
	ОбновитьНадписи();
КонецПроцедуры	           

// ===============================
Процедура ИзмДатаДок()
	глПриИзмененииДатыДокумента(Контекст, СтараяДата);
КонецПроцедуры


// ===============================
Процедура ВводНаОсновании(ДокОсн)
	Если (ДокОсн.Валюта <> Гривня) Тогда
	    Предупреждение("Выписка Приложения №2 предусмотрена только при продаже товаров в гривнях!");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	Фирма = ДокОсн.Фирма;
	Контрагент = ДокОсн.Контрагент;
	ТипУчета=ДокОсн.ТипУчета;
	ДокументОснование = ДокОсн;
	Договор = ДокОсн.Договор;
	Основание = ДокОсн.Основание;

	УсловиеПродажи = ДокОсн.УсловиеПродажи;
	ФормаРасчетов = ДокОсн.ФормаРасчетов;
	
	СтавкаНДС = ДокОсн.СтавкаНДС;
	Валюта = ДокОсн.Валюта;
	Дата_Курса= ДокОсн.Дата_Курса;
	Курс = ДокОсн.Курс;
	
	ДокОсн.ВыбратьСтроки();
	Пока ДокОсн.ПолучитьСтроку() <> 0 Цикл
		НоваяСтрока();
		Товар = ДокОсн.Товар;
		Единица = ДокОсн.Единица;
		Количество = ДокОсн.Количество;  	
		ЦенаБезНДС = ДокОсн.ЦенаБезНДС;
		Коэффициент = ДокОсн.Коэффициент;
	КонецЦикла;
	
	глУстановитьНомерДок(Контекст);
	
КонецПроцедуры

// ===============================
Процедура ВычИзмНДС()
	ИзмНДС = (ЦенаБезНДС * ИзмКоличество +  Количество * ИзмЦеныБезНДС)*глПроцентНДС(СтавкаНДС)/100;
	ИзмСуммыБезНДС = (ЦенаБезНДС * ИзмКоличество + Количество * ИзмЦеныБезНДС);
КонецПроцедуры

// ===============================
Процедура ПриОткрытии() 
	НачальнаяДатаДокумента = ДатаДок;
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кнОК.Доступность(0);
		Форма.кнПровести.Доступность(0);
		Форма.кнДействия.Доступность(0);
		
		Форма.кнОснование.Доступность(0);
	Иначе
		Форма.КнопкаПоУмолчанию("кнОК");
	КонецЕсли;	
	ПриЗаписиПерепроводить(1);
	ОбновитьНадписи();
КонецПроцедуры

// ===============================
Процедура ИзмИзмКоличество()
	// При изменении количества
	Если ИзмКоличество <> 0 Тогда
		Если ИзмЦеныБезНДС <> 0 Тогда
		    ИзмЦеныБезНДС = 0;
		    Предупреждение("Разрешается корректировать или цену или количество!");
		КонецЕсли;
	КонецЕсли;
	ВычИзмНДС();
КонецПроцедуры

// ===============================
Процедура ИзмИзмЦеныБезНДС()
	Если ИзмЦеныБезНДС <> 0 Тогда
		Если ИзмКоличество <> 0 Тогда
		    ИзмКоличество = 0;
		    Предупреждение("Разрешается корректировать или цену или количество!");
		КонецЕсли;
	КонецЕсли;
	ВычИзмНДС();
КонецПроцедуры

// ===============================
Процедура Печать()
	
	Фирма.ИспользоватьДату(ДатаДок);
	ФирмаНалогНомер = Фирма.ИНН;
	КонтрагентНалогНомер = Контрагент.ИНН;

	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("ТаблицаУ");
	
	Для Страница = 1 По 2 Цикл
		Таб.ВывестиСекцию( "Пустота" );
		Если Страница = 1 Тогда
			Таб.ВывестиСекцию( "Оригинал" );
		ИначеЕсли Страница = 2 Тогда
			Таб.ВывестиСекцию( "Копия" );
		КонецЕсли;

		Таб.ВывестиСекцию( "Шапка" );

		ВыбратьСтроки();
		Пока ( ПолучитьСтроку() > 0 ) Цикл
			Ст1 = ДатаДок;
			Ст2 = Причина;
			Ст3 = Товар.ПолнНаименование;
			Ст4 = Единица;
			Ст5 = "Х";
			Ст6 = "Х";
		       Ст7 = "Х";
                    Ст8 = "Х";
			Если ИзмКоличество<>0 Тогда
				Ст5 = Формат(ИзмКоличество,"Ч13.3");
				Ст6 = глФРМ3(глПересчет(ЦенаБезНДС,Валюта,Курс,Гривня,ДатаДок),Гривня,0);
			Иначе
				Ст7 = глФРМ3(глПересчет(ИзмЦеныБезНДС,Валюта,Курс,Гривня,ДатаДок),Гривня,0);
				Ст8 = Формат(Количество,"Ч13.3");
			КонецЕсли;
			Ст9 = "Х";
			Ст10 = "Х";
		       Ст11 = "Х";
      		    Ст12 = "Х";
			Ст13 = "Х";
		       Ст14 = "Х";
			Ст15 = "Х";
			ПечСуммыБезНДС = глФРМ3(глПересчет(ИзмСуммыБезНДС,Валюта,Курс,Гривня,ДатаДок),Гривня,0);
			Если СтавкаНДС = Перечисление.ЗначенияНДС.ОсновнаяСтавкаНДС Тогда
			    Ст9 = ПечСуммыБезНДС;
			ИначеЕсли СтавкаНДС = Перечисление.ЗначенияНДС.ЛьготнаяСтавкаНДС Тогда
				Ст10 = ПечСуммыБезНДС;
			ИначеЕсли СтавкаНДС = Перечисление.ЗначенияНДС.БезНДС Тогда
				Ст11 = ПечСуммыБезНДС;
			КонецЕсли;
			
			ПечИзмНДС = глФРМ3(глПересчет(ИзмНДС,Валюта,Курс,Гривня,ДатаДок),Гривня,0);
			Если (СтавкаНДС = Перечисление.ЗначенияНДС.ОсновнаяСтавкаНДС) и (ИзмСуммыБезНДС<0) Тогда
				Ст12 = ПечИзмНДС;
				Ст13 = ПечИзмНДС;
			ИначеЕсли (СтавкаНДС = Перечисление.ЗначенияНДС.ОсновнаяСтавкаНДС) и (ИзмСуммыБезНДС>0) Тогда
				Ст14 = ПечИзмНДС;
				Ст15 = ПечИзмНДС;
			КонецЕсли;

			Таб.ВывестиСекцию("Строка");
		КонецЦикла;
		Таб.ВывестиСекцию("Подвал");

		Если Страница <> 2 Тогда
			Таб.НоваяСтраница();
		КонецЕсли;

	КонецЦикла;

	Таб.Опции(0, 0, 0, 0);
	Таб.ПараметрыСтраницы(2,,,,,,,,);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Печать расчета корректировки", "");
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	Если глМожноЗаписатьДокумент(Контекст)=0 Тогда
		СтатусВозврата(0);                        
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры

Процедура ВыборФирмы()
	// по кнопке редактирования параметров фирмы в докумнете
	Перем КонтекстДокумента;
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ИнформацияОфирме", КонтекстДокумента);
	ОбновитьНадписи();
КонецПроцедуры	

//*****************************************************************************
Процедура ПриНачалеВыбораЗначения(Рекв,Флаг)
	Флаг=0;
	Если Рекв="Фирма" Тогда
		ВыборФирмы();
	Иначе
		Флаг=1;
	КонецЕсли;
КонецПроцедуры

//*****************************************************************************
//Инициализирум список действий по кнопке "Действия"      
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ДвиженияДокумента,
	|ОткрытьВЖурнале,
	|ИзмКомментария");
