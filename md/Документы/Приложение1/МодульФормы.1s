
Перем СтараяДата;
Перем НачальнаяДатаДокумента;

// ===============================
Процедура ВводНового()
	Предупреждение("Этот документ можно вводить только на основании Налоговой накладной!");
	СтатусВозврата(0);
КонецПроцедуры
                     
// ===============================
Процедура ИзмДатаДок()
	глПриИзмененииДатыДокумента(Контекст, СтараяДата);
КонецПроцедуры

// ===============================
Процедура ИзмКоличество()
	КоличествоОст = Количество - КоличествоОтгр;
	СуммаБезНДСОтгр = ЦенаБезНДС*КоличествоОтгр;
	НДСОтгр = СуммаБезНДСОтгр*глПроцентНДС(СтавкаНДС)/100;
КонецПроцедуры

//*****************************************************************************
Функция ЗаголовокФормы() 
	Перем Заголовок, Название;
	Заголовок = "Приложение № 1 к налоговой накладной ";
	Название ="Приложение 1 №"; 
	
	Заголовок = Заголовок + глЗаголовок(Контекст);
	
	Форма.Заголовок(Заголовок);     
	Возврат Название;
КонецФункции //ЗаголовокФормы

//******************************************************************************
Процедура ОбновитьНадписи()
	Форма.ТекстОснования.Заголовок(СокрП(Основание));
КонецПроцедуры

//*****************************************************************************
Процедура ВыборОснования()
	// Процедура по кнопке редактирования основания в докумнете
	Перем КонтекстДокумента;
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ОснованиеДокумента", КонтекстДокумента);
	// Могли изменить Контрагента
	ОбновитьНадписи();
КонецПроцедуры	

// ===============================
Процедура ВводНаОсновании(ДокОсн)
	Если (ДокОсн.Валюта <> Гривня) Тогда
	    Предупреждение("Выписка Приложения №1 предусмотрена только при продаже товаров в гривнях!");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	Фирма = ДокОсн.Фирма;  
	Автор = глПользователь;  	
	Контрагент = ДокОсн.Контрагент;
	ТипУчета=ДокОсн.ТипУчета;
	ДокументОснование = ДокОсн;
	Договор = ДокОсн.Договор;
	Основание = ДокОсн.Основание;

	УсловиеПродажи = ДокОсн.УсловиеПродажи;
	ФормаРасчетов = ДокОсн.ФормаРасчетов;
	
	СтавкаНДС = ДокОсн.СтавкаНДС;
	Валюта = ДокОсн.Валюта;
	Дата_Курса= ДокОсн.Дата_Курса;
	Курс = ДокОсн.Курс;
	

	ДокОсн.ВыбратьСтроки();
	Пока ДокОсн.ПолучитьСтроку() <> 0 Цикл
		НоваяСтрока();
		Товар = ДокОсн.Товар;
		Единица = ДокОсн.Единица;
		Количество = ДокОсн.Количество;  	
		КоличествоОтгр = ДокОсн.Количество;
		ЦенаБезНДС = ДокОсн.ЦенаБезНДС;
		Коэффициент = ДокОсн.Коэффициент;
		СуммаБезНДС = ДокОсн.СуммаБезНДС;
		
		ИзмКоличество();
	КонецЦикла;
	
	глУстановитьНомерДок(Контекст);
КонецПроцедуры

// ===============================

Процедура ПриОткрытии() 
	НачальнаяДатаДокумента = ДатаДок;
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кнОК.Доступность(0);
		Форма.кнПровести.Доступность(0);
		Форма.кнДействия.Доступность(0);
		
		Форма.кнОснование.Доступность(0);
	Иначе
		Форма.КнопкаПоУмолчанию("кнОК");
	КонецЕсли;	
	ПриЗаписиПерепроводить(1);   
	ОбновитьНадписи();
КонецПроцедуры

// ===============================
Процедура ИзмТовар()
	глПриИзмененииТовара(Контекст);
	Если Товар.Выбран()=1 Тогда
		Количество = 0;
		КоличествоОтгр = 0;
		ИзмКоличество();
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура Печать()
	
	ФДата = Лев(Формат(ДатаДок, "ДДДММММГГГГ"), (СтрДлина(Формат(ДатаДок, "ДДДММММГГГГ"))-2));//отсёк "г."
	
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("ТаблицаУ");
	
	Для Страница = 1 По 2 Цикл
		Таб.ВывестиСекцию( "Пустота" );
		Если Страница = 1 Тогда
			Таб.ВывестиСекцию( "Оригинал" );
		ИначеЕсли Страница = 2 Тогда
			Таб.ВывестиСекцию( "Копия" );
		КонецЕсли;
		
		Таб.ВывестиСекцию( "Шапка" );
		Ном = 1;
		Ист7 = 0;
		ВыбратьСтроки();
		Пока ( ПолучитьСтроку() > 0 ) Цикл
			Ст1 = Ном;
			Ном = Ном + 1;                    
			Ст2 = ?(Договор.Выбран()=1,Строка(Договор),СокрЛП(ДокументОснование.Основание)) + ", " + СокрЛП(Контрагент.ПолнНаименование);
			Ст3 = Товар.ПолнНаименование;
			Ст4 = Единица;
			Ст5 = глФРМ3(глПересчет(ЦенаБезНДС,Валюта,Курс,Гривня,ДатаДок),Гривня,0);
			Ст6 = Формат(Количество,"Ч13.2");
			Ст7 = глФРМ3(глПересчет(ЦенаБезНДС*Количество,Валюта,Курс,Гривня,ДатаДок),Гривня,0);
			Ист7 = Ист7 + ЦенаБезНДС*Количество;
			Ст8 = Товар.ПолнНаименование;
			Ст9 = Единица;
			Ст10 = Формат(КоличествоОтгр,"Ч13.2");
			Ст11 = Товар.ПолнНаименование;
			Ст12 = Единица;
			Ст13 = Формат(КоличествоОст,"Ч13.2");
			
			Таб.ВывестиСекцию("Строка");
		КонецЦикла;                                        
		Ист7 = глФРМ3(глПересчет(Ист7,Валюта,Курс,Гривня,ДатаДок),Гривня,0);
		Таб.ВывестиСекцию("Подвал");
		Если Страница <> 2 Тогда
			Таб.НоваяСтраница();
		КонецЕсли;
		
	КонецЦикла;
	
	Таб.Опции(0, 0, 0, 0);
	Таб.ПараметрыСтраницы(2,,,,,,,,);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Печать расчета корректировки", "");
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	Если глМожноЗаписатьДокумент(Контекст)=0 Тогда
		СтатусВозврата(0);                        
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры

Процедура ВыборФирмы()
	// по кнопке редактирования параметров фирмы в докумнете
	Перем КонтекстДокумента;
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ИнформацияОфирме", КонтекстДокумента);
	ОбновитьНадписи();
КонецПроцедуры	

//*****************************************************************************
Процедура ПриНачалеВыбораЗначения(Рекв,Флаг)
	Флаг=0;
	Если Рекв="Фирма" Тогда
		ВыборФирмы();
	Иначе
		Флаг=1;
	КонецЕсли;
КонецПроцедуры
                                                                 
Форма.Товар.ВыполнятьФормулуТолькоПриИзменении(1);

//*****************************************************************************
//Инициализирум список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ДвиженияДокумента,
	|ОткрытьВЖурнале,
	|ИзмКомментария");
