Перем НачальнаяДатаДокумента;

//*****************************************************************************
// "служебные" функции и процедуры 

//*****************************************************************************
Функция ЗаголовокФормы() 
	Перем Заголовок, Название;
	Заголовок = "Договор ";
	Название ="Договор №";
	
	Заголовок = Заголовок + глЗаголовок(Контекст);
	
	Форма.Заголовок(Заголовок);     
	Возврат Название;
КонецФункции //ЗаголовокФормы

// ===============================
Процедура УстДоступность()
	Если Выбран()=0 Тогда
	    Возврат;
	КонецЕсли;
	// Проверяем были ли движения по взаиморасчетам с данным договором
	
	фЕстьДвижения=0;
	
	РегВзаим=СоздатьОбъект("Регистр.ВзаиморасчетыПоставщиков");
	РегВзаим.УстановитьЗначениеФильтра("Договор",ТекущийДокумент(),1);
	РегВзаим.ВыбратьДвижения('01.01.2000',,);
	Если РегВзаим.ПолучитьДвижение()=1 Тогда
		фЕстьДвижения=1;
	КонецЕсли;
	РегВзаим=0;
	Если фЕстьДвижения=0 Тогда
		РегВзаим=СоздатьОбъект("Регистр.ВзаиморасчетыПокупателей");
		РегВзаим.УстановитьЗначениеФильтра("Договор",ТекущийДокумент(),1);
		РегВзаим.ВыбратьДвижения('01.01.2000',,);
		Если РегВзаим.ПолучитьДвижение()=1 Тогда
			фЕстьДвижения=1;
		КонецЕсли;
		РегВзаим=0;
	КонецЕсли;                           
	                             
	Если фЕстьДвижения=1 Тогда
		Форма.СпособФормированияНалоговыхДокументов.Доступность(0);
		Форма.СпособФормированияНалоговыхДокументов1.Доступность(0);  
		Форма.ВидТорговли.Доступность(0);
		Форма.рСпособ.Доступность(0);
	КонецЕсли;
	
	ДокП = СоздатьОбъект("Документ");
	Если ДокП.ВыбратьПодчиненныеДокументы(,ТекущаяДата(),ТекущийДокумент()) = 1 тогда
		Пока ДокП.ПолучитьДокумент() = 1 цикл
			Если ДокП.Вид() = "ДополнениеДоговораКонсигнации" тогда
				Форма.ВидТорговли.Доступность(0);
				Прервать;
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;	
	
КонецПроцедуры

//******************************************************************************
Процедура ОбновитьНадписи()
	Форма.ТекстВалюты.Заголовок(глСтрокаВалюты(Контекст));
КонецПроцедуры

//*****************************************************************************
Процедура ВыборФирмы()
	// по кнопке редактирования параметров фирмы в докумнете
	Перем КонтекстДокумента;
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ИнформацияОфирме", КонтекстДокумента);
	ОбновитьНадписи();
КонецПроцедуры

//*****************************************************************************
Процедура ВыборОплаты()
	// Процедура по кнопке редактирования параметров оплаты в докумнете
	Перем КонтекстДокумента;
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ИнформацияОценах", КонтекстДокумента);
	ОбновитьНадписи();
КонецПроцедуры	


//*****************************************************************************
Процедура ПриНачалеВыбораЗначения(Рекв,Флаг)	
	Если Рекв="Фирма" Тогда
		ВыборФирмы();
		Флаг=0;
	КонецЕсли;
КонецПроцедуры

//******************************************************************************
Процедура ВводНаОсновании(ДокОснование) //Предопределенная процедура
	Предупреждение("Договор нельзя вводить на основании!");
	СтатусВозврата(0);
КонецПроцедуры
                             
//******************************************************************************
Процедура ВводНового(Скопирован) //Предопределенная процедура

	глЗаполнитьШапку(Контекст);
	Если Скопирован=1 Тогда	//копирование документа
		Возврат;
	КонецЕсли;

	ДатаДок=РабочаяДата();
	ДатаНачала=ДатаДок;
	
	Контрагент=Константа.ОсновнойПокупатель;
	//ИзмКонтрагент();
	
	СпособФормированияНалоговыхДокументов=1;
	
	Валюта=Константа.ОсновнаяВалютаПродажи;
	Дата_Курса = ДатаДок;
	Если ПустоеЗначение(Валюта) = 1 Тогда
		Валюта = Гривня;
	КонецЕсли;
	Курс=глКурсДляВалюты(Валюта,Дата_Курса);

	Если ПустоеЗначение(ВидТорговли)=1 Тогда
		ВидТорговли = Перечисление.ВидыТорговли.Предоплата;
	КонецЕсли;
	
КонецПроцедуры

//*****************************************************************************
Процедура ПриОткрытии() //Предопределенная процедура
	
	НачальнаяДатаДокумента = ДатаДок;
	глПроверкаРазрешенияРедактирования(Контекст);

	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кнОК.Доступность(0);
		Форма.кнЗаписать.Доступность(0);
		Форма.кнДействия.Доступность(0);
		
		Форма.кнВалюта.Доступность(0);
	Иначе
		Форма.КнопкаПоУмолчанию("кнОК");
	КонецЕсли;
	
    ОбновитьНадписи();

	УстДоступность();
КонецПроцедуры //При открытии

//*****************************************************************************
Процедура ПриЗаписи() //Предопределенная процедура
	Если глМожноЗаписатьДокумент(Контекст)=0 Тогда
		СтатусВозврата(0);                        
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры

//*****************************************************************************
//Инициализирум список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
	|СтруктураПодчиненности,
	|ВводНаОсновании,
	|ОткрытьВЖурнале,
	|Подчиненные,
	|ИзмКомментария");
