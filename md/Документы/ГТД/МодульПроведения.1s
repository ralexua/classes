Перем спФирмы;
// ===============================
Функция ПроверкаШапки()
	глВсеВыбрано = 1;
	глВыбранЛи(Фирма,"Фирма");
	глВыбранЛи(Контрагент,"Контрагент");
	глВыбранЛи(КатегорияЦен,"Категория цен");
	глВыбранЛи(Склад,"Склад");
	глВыбранЛи(Валюта,"Валюта");
	глВыбранЛи(Таможня,"Таможня");
    Если Перевозчик.Выбран()=1 Тогда
    	глВыбранЛи(ВалютаПеревозки,"Валюта перевозки");
    КонецЕсли;
	Возврат глВсеВыбрано;
КонецФункции

// ===============================
Функция ПроверкаСтроки()
    глВсеВыбрано = 1;
	глВыбранЛи(Товар,"Товар",НомерСтроки);
	глВыбранЛи(Единица,"Единица",НомерСтроки);
	Возврат глВсеВыбрано;
КонецФункции                 

// ===============================
Процедура ДвиженияВзаиморасчеты()
    
	ФлагОтгрузки = 1;	
	ЗнакОплаты = +1; 
	ФлагВозврата = 0;
	
	тбСуммыПогашения = СоздатьОбъект("ТаблицаЗначений");
	тбСуммыПогашения.НоваяКолонка("СтавкаНДС");
	тбСуммыПогашения.НоваяКолонка("СуммаСНДС");
	тбСуммыПогашения.НоваяКолонка("СтавкаВознаграждения","Число",8,5);
	
	ВремРег = СоздатьОбъект("Регистры");
	                                                      
	ВидТорговли = Перечисление.ВидыТорговли.Предоплата;
	
	// Взаиморасчеты с поставщиком - расчеты с нулевой ставкой НДС, ВР вычисляются по первому событию
	спДоговора = глПолучитьСписокКредДоговоров(Контекст);
	глРассчитатьИтогиВзаиморасчетов(Контекст,ВремРег,спФирмы,ЗнакОплаты, Контрагент, спДоговора);
			
	Для Инд=1 по спФирмы.РазмерСписка() Цикл
		текФирма = спФирмы.ПолучитьЗначение(Инд);
				
		тбСуммыПогашения.УдалитьСтроки();
		тбСуммыПогашения.НоваяСтрока();
		Если ПустоеЗначение(текФирма)=0 Тогда
			тбСуммыПогашения.СтавкаНДС = Перечисление.ЗначенияНДС.ЛьготнаяСтавкаНДС;
			ВВ = Гривня;
		Иначе
			ВВ = глВалютаВзаиморасчетов(Контрагент);
		КонецЕсли;
		тбСуммыПогашения.СуммаСНДС = глПодготовитьСумму(Итог("СуммаБезНДС"),Валюта,Курс,ВВ,Дата_Курса);

		тбДолги = глПолучитьИтогиВзаиморасчетов(Контекст,ВремРег, текФирма, ЗнакОплаты, Контрагент, спДоговора);
		глПровестиПоВзаиморасчетам(Контекст,  ФлагОтгрузки, ЗнакОплаты, ФлагВозврата, текФирма, тбДолги, тбСуммыПогашения, Контрагент, спДоговора, ВидТорговли, ДокументОснование);
	КонецЦикла;
	
	// Взаиморасчеты с таможней - расчеты с нулевой ставкой НДС, ВР вычисляются по первому событию
	спДоговора = СоздатьОбъект("СписокЗначений");
	спДоговора.ДобавитьЗначение(ДоговорТаможни);
	глРассчитатьИтогиВзаиморасчетов(Контекст,ВремРег,спФирмы,ЗнакОплаты, Таможня, спДоговора);
			
	Для Инд=1 по спФирмы.РазмерСписка() Цикл
		текФирма = спФирмы.ПолучитьЗначение(Инд);
		
		// взаиморасчеты с таможней по услугам 		
		тбСуммыПогашения.УдалитьСтроки();
		тбСуммыПогашения.НоваяСтрока();
		Если ПустоеЗначение(текФирма)=0 Тогда
			тбСуммыПогашения.СтавкаНДС = Перечисление.ЗначенияНДС.ЛьготнаяСтавкаНДС;
			ВВ = Гривня;
		Иначе
			ВВ = глВалютаВзаиморасчетов(Таможня);
		КонецЕсли;
		тбСуммыПогашения.СуммаСНДС = глПодготовитьСумму(Итог("СуммаУслуг"),Валюта,Курс,ВВ,Дата_Курса);
		
		// взаиморасчеты с таможней по пошлине
		тбСуммыПогашения.НоваяСтрока();
		Если ПустоеЗначение(текФирма)=0 Тогда
			тбСуммыПогашения.СтавкаНДС = Перечисление.ЗначенияНДС.ЛьготнаяСтавкаНДС;
			ВВ = Гривня;
		Иначе
			ВВ = глВалютаВзаиморасчетов(Таможня);
		КонецЕсли;
		тбСуммыПогашения.СуммаСНДС = глПодготовитьСумму(Итог("СуммаПошлины"),Валюта,Курс,ВВ,Дата_Курса);

		тбДолги = глПолучитьИтогиВзаиморасчетов(Контекст,ВремРег, текФирма, ЗнакОплаты, Таможня, спДоговора);
		глПровестиПоВзаиморасчетам(Контекст,  ФлагОтгрузки, ЗнакОплаты, ФлагВозврата, текФирма, тбДолги, тбСуммыПогашения, Таможня, спДоговора, ВидТорговли, КредДокументТаможни);
		
	КонецЦикла;
	    
	// Взаиморасчеты с перевозчиком - расчеты с нулевой ставкой НДС, ВР вычисляются по первому событию
    
	Если Перевозчик.Выбран()=1 Тогда
		спДоговора = СоздатьОбъект("СписокЗначений");
		спДоговора.ДобавитьЗначение(ДоговорПеревозки);
		глРассчитатьИтогиВзаиморасчетов(Контекст,ВремРег,спФирмы,ЗнакОплаты, Перевозчик, спДоговора);
		
		Для Инд=1 по спФирмы.РазмерСписка() Цикл
			текФирма = спФирмы.ПолучитьЗначение(Инд);
			
			тбСуммыПогашения.УдалитьСтроки();
			тбСуммыПогашения.НоваяСтрока();
			Если ПустоеЗначение(текФирма)=0 Тогда
				тбСуммыПогашения.СтавкаНДС = Перечисление.ЗначенияНДС.ЛьготнаяСтавкаНДС;
				ВВ = Гривня;
			Иначе
				ВВ = глВалютаВзаиморасчетов(Перевозчик);
			КонецЕсли;
			тбСуммыПогашения.СуммаСНДС = глПодготовитьСумму(Итог("СуммаПеревозки"),Валюта,Курс,ВВ,Дата_Курса);
			
			тбДолги = глПолучитьИтогиВзаиморасчетов(Контекст,ВремРег, текФирма, ЗнакОплаты, Перевозчик, спДоговора);
			глПровестиПоВзаиморасчетам(Контекст,  ФлагОтгрузки, ЗнакОплаты, ФлагВозврата, текФирма, тбДолги, тбСуммыПогашения, Перевозчик, спДоговора, ВидТорговли, КредДокументПеревозки);		
		КонецЦикла;
	КонецЕсли;
	
	Для Инд=1 по спФирмы.РазмерСписка() Цикл
		текФирма = спФирмы.ПолучитьЗначение(Инд);
		
		Если ПустоеЗначение(КредДокументТаможни)=1 Тогда
			КДТаможни = ТекущийДокумент();
		Иначе
			КДТаможни = КредДокументТаможни;
		КонецЕсли;
		Если ПустоеЗначение(текФирма)=1 Тогда
			ВВ = глВалютаВзаиморасчетов(Таможня);
		Иначе
			ВВ = Гривня;
		КонецЕсли;
		// Учет НДС на импорт - приходуем от таможни
		СтавкаНДС = Перечисление.ЗначенияНДС.ОсновнаяСтавкаНДС;
		СуммаНДС = Итог("СуммаСНДС")-Итог("СуммаБезНДС");
		
		СуммаСНДС_Т = глПересчет(СуммаНДС,Валюта,Курс,ВВ,Дата_Курса);
		СуммаСНДС_НУ = СуммаСНДС_Т * (100 + глПроцентНДС(СтавкаНДС))/глПроцентНДС(СтавкаНДС);
		
		Если СуммаСНДС_Т <> 0 Тогда
			Если ПустоеЗначение(текФирма)=1 Тогда
				Регистр.ВзаиморасчетыПоставщиков.ДвижениеРасход(текФирма,Таможня,ДоговорТаможни,0,
					КДТаможни,СуммаСНДС_Т,НачислениеНДСнаИмпорт,0,"");
			Иначе	
				Регистр.ВзаиморасчетыПоставщиков.ДвижениеРасход(текФирма,Таможня,ДоговорТаможни,СтавкаНДС,
					КДТаможни,СуммаСНДС_Т,НачислениеНДСнаИмпорт,0,0);
					
				Если ПустоеЗначение(ДокументОснование)=1 Тогда
				    КДокумент = ТекущийДокумент();
				Иначе
					КДокумент = ДокументОснование;
				КонецЕсли;
				Регистр.ВзаиморасчетыПоставщиков.ДвижениеРасход(текФирма,Контрагент,Договор,СтавкаНДС,
					КДокумент,0,НачислениеНДСнаИмпорт,-СуммаСНДС_НУ,НУ_ПервоеСобытие);
			КонецЕсли;
		КонецЕсли;
		
		// Учет акцизного сбора - приходуем от таможни
		СтавкаНДС = Перечисление.ЗначенияНДС.ЛьготнаяСтавкаНДС;
		СуммаСНДС_Т = глПересчет(Итог("СуммаАкциза"),Валюта,Курс,ВВ,Дата_курса);
		СуммаСНДС_НУ = СуммаСНДС_Т;
		Если СуммаСНДС_Т <> 0 Тогда
			Если ПустоеЗначение(текФирма)=1 Тогда                  
					Регистр.ВзаиморасчетыПоставщиков.ДвижениеРасход(текФирма,Таможня,ДоговорТаможни,0,
						КДТаможни,СуммаСНДС_Т,НачислениеАкциза,"","");
			Иначе	      
				Регистр.ВзаиморасчетыПоставщиков.ДвижениеРасход(текФирма,Таможня,ДоговорТаможни,СтавкаНДС,
					КДТаможни,СуммаСНДС_Т,НачислениеАкциза,-СуммаСНДС_НУ,"");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// ===============================
Процедура ДвиженияОстатки()

	ФлагПрихода = 1;
	
	ФлагВозврата = 0;
	
	глИзменитьОстатки(Контекст, спФирмы, Склад, ФлагВозврата, ФлагПрихода);
	
КонецПроцедуры
                           
// ===============================
Процедура ДвиженияПартии() 
	
	Для Инд=1 по спФирмы.РазмерСписка() Цикл
		текФирма = спФирмы.ПолучитьЗначение(Инд);
		
		Если ПустоеЗначение(текФирма)=1 Тогда
			МетодРасчетаСебестоимости = Константа.МетодРасчетаСебестоимостиУправленческогоУчета;
		Иначе
			текФирма.ИспользоватьДату(ДатаДок);
			МетодРасчетаСебестоимости = текФирма.МетодРасчетаСебестоимостиФинансовогоУчета;
		КонецЕсли;                  
		
		// Рассчитать сумму услуг в табличной части
		ИСуммаСНДСУслуг = 0;
		ИСуммаБезНДСУслуг = 0;
		ИСуммаCНДСТоваров = 0;
		ИСуммаБезНДСТоваров = 0;
		НомерСтрокиПоследнегоТовара = 1;
		
		ВыбратьСтроки();
		Пока ПолучитьСтроку()=1 Цикл
			Если Товар.УслугиНаСебестоимость = 1 Тогда
				ИСуммаСНДСУслуг = ИСуммаСНДСУслуг + СуммаСНДС;
				ИСуммаБезНДСУслуг = ИСуммаБезНДСУслуг + СуммаБезНДС;
			Иначе
				ИСуммаCНДСТоваров = ИСуммаCНДСТоваров + СуммаСНДС;
				ИСуммаБезНДСТоваров = ИСуммабезНДСТоваров + СуммаБезНДС;				
				НомерСтрокиПоследнегоТовара = НомерСтроки;
			КонецЕсли;
		КонецЦикла;
		
		Если (ИСуммаСНДСУслуг = Итог("СуммаСНДС")) и (ИСуммаСНДСУслуг <> 0) Тогда
			глНеПроводить(Контекст,"Отсутствуют товары в табличной части.");
			Возврат;
		КонецЕсли;		
		
		НераспрИСуммаСНДСУслуг = ИСуммаСНДСУслуг;
		НераспрИСуммаБезНДСУслуг = ИСуммаБезНДСУслуг;
		
		Регистр.ПартииТоваров.Фирма = текФирма;
		Регистр.ПартииТоваров.Статус = Купленный;
		
		Если МетодРасчетаСебестоимости = Перечисление.МетодыРасчетаСебестоимости.ПоСреднему Тогда
			Регистр.ПартииТоваров.Контрагент = 0;
			Регистр.ПартииТоваров.Поставщик = 0;
			Регистр.ПартииТоваров.Поставка = 0;
			Регистр.ПартииТоваров.ПрихДокумент = 0;
		Иначе
			Регистр.ПартииТоваров.Контрагент = Контрагент;
			Регистр.ПартииТоваров.Поставщик = Контрагент;
			Регистр.ПартииТоваров.Поставка = ТекущийДокумент();
			Регистр.ПартииТоваров.ПрихДокумент = ТекущийДокумент();
		КонецЕсли;
		
		ВыбратьСтроки();
		Пока ПолучитьСтроку()=1 Цикл
			
			Если Товар.УслугиНаСебестоимость = 1 Тогда
				Продолжить;
			КонецЕсли;
			
			Регистр.ПартииТоваров.ПривязыватьСтроку(НомерСтроки);
			Регистр.ПартииТоваров.Товар = Товар;
			Регистр.ПартииТоваров.ОстатокТовара = Количество * Коэффициент;
			
			Если НомерСтроки = НомерСтрокиПоследнегоТовара Тогда
				ДопСуммаБезНДС = НераспрИСуммаБезНДСУслуг;
				ДопСуммаБезНДС = НераспрИСуммаБезНДСУслуг;
			Иначе                                         
				ДопСуммаБезНДС = СуммаБезНДС / ИСуммаБезНДСТоваров *  ИСуммаБезНДСУслуг;
				НераспрИСуммаБезНДСУслуг = НераспрИСуммаБезНДСУслуг  - ДопСуммаБезНДС; 
				                                                                        
				ДопСуммаСНДС = СуммаСНДС / ИСуммаCНДСТоваров *  ИСуммаСНДСУслуг;
				НераспрИСуммаСНДСУслуг = НераспрИСуммаСНДСУслуг  - ДопСуммаСНДС;
			КонецЕсли;
			
			Если ПустоеЗначение(текФирма)=1 Тогда
				ВалютаУчета = Товар.ВалютаУчета;
			Иначе
				ВалютаУчета = Гривня;
			КонецЕсли;
			
			Регистр.ПартииТоваров.Стоимость = глПересчет(СуммаБезНДС + ДопСуммаБезНДС,Валюта,Курс,ВалютаУчета,Дата_Курса);
			Регистр.ПартииТоваров.НДС = глПересчет(СуммаСНДС + ДопСуммаСНДС - СуммаБезНДС - ДопСуммаБезНДС,Валюта,Курс,ВалютаУчета,Дата_Курса);
			
			Регистр.ПартииТоваров.ПродСтоимость = 0;			
			
			Регистр.ПартииТоваров.Оборот =0;
			Регистр.ПартииТоваров.НДСРасхода = 0;
			
			Если Товар.ВидТовара = Перечисление.ВидыТоваров.Услуга Тогда
				Регистр.ПартииТоваров.Статус = Услуга;
				Регистр.ПартииТоваров.КодОперации = ПокупкаУслуги;
				Регистр.ПартииТоваров.ДвижениеПриходВыполнить();
				Регистр.ПартииТоваров.ДвижениеРасходВыполнить();
			Иначе     
				Регистр.ПартииТоваров.КодОперации = ЗакупкаТовара;
				Регистр.ПартииТоваров.ДвижениеПриходВыполнить();
			КонецЕсли;
			        
			Регистр.ПартииТоваров.НДС = 0;
			Регистр.ПартииТоваров.ОстатокТовара = 0;
			
			// Акциз
			Если СуммаАкциза <> 0 Тогда
				Регистр.ПартииТоваров.Стоимость = глПересчет(СуммаАкциза,Валюта,Курс,ВалютаУчета,Дата_Курса);
				Регистр.ПартииТоваров.КодОперации = ПриходованиеАкциз;
				Регистр.ПартииТоваров.ДвижениеПриходВыполнить();
			КонецЕсли;	
			        
			// Пошлина
			Если СуммаПошлины <> 0 Тогда
				Регистр.ПартииТоваров.Стоимость = глПересчет(СуммаПошлины,Валюта,Курс,ВалютаУчета,Дата_Курса);
				Регистр.ПартииТоваров.КодОперации = ПриходованиеПошлина;
				Регистр.ПартииТоваров.ДвижениеПриходВыполнить();
			КонецЕсли;	
			
			// Таможенные услуги
			Если СуммаУслуг <> 0 Тогда
				Регистр.ПартииТоваров.Стоимость = глПересчет(СуммаУслуг,Валюта,Курс,ВалютаУчета,Дата_Курса);
				Регистр.ПартииТоваров.КодОперации = ПриходованиеУслугиТаможни;
				Регистр.ПартииТоваров.ДвижениеПриходВыполнить();              
			КонецЕсли;	
			
			// Перевозка
			Если (Перевозчик.Выбран()=1) и (СуммаПеревозки <> 0) Тогда				
				Регистр.ПартииТоваров.Стоимость = глПересчет(СуммаПеревозки,Валюта,Курс,ВалютаУчета,Дата_Курса);
				Регистр.ПартииТоваров.КодОперации = ПриходованиеПеревозка;
				Регистр.ПартииТоваров.ДвижениеПриходВыполнить();
			КонецЕсли;
			
		КонецЦикла; // строки документа
	КонецЦикла; // Фирмы
КонецПроцедуры
//-------------------------------------
Процедура ОбработкаПроведения()

	Если ДатаДок>ТекущаяДата() Тогда
		глНеПроводить(Контекст,"Нельзя проводить документ будущей датой!");
		Возврат;
	КонецЕсли;
	
	Если глПроверкаДублейСтрок(Контекст)=1 Тогда
		глНеПроводить(Контекст,"В документе строки с одинаковым товаром, но разной ценой!");
		Возврат;
	КонецЕсли;

	Если ПроверкаШапки() = 0 Тогда
	    глНеПроводить(Контекст); 
		Возврат;
	КонецЕсли;
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		Если ПроверкаСтроки() = 0 Тогда
			глНеПроводить(Контекст); 
			Возврат;
		КонецЕсли;
	КонецЦикла;
    
	спФирмы = глПолучитьФирмы(Контекст);
	                
	Если спФирмы.РазмерСписка()>0 Тогда
		
		ДвиженияВзаиморасчеты();
		
		ДвиженияОстатки();
		
		ДвиженияПартии();
		
	КонецЕсли; 
	
		

КонецПроцедуры


