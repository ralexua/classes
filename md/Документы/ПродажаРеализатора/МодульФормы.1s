//*****************************************************************************
// Описание переменных 
Перем СписокДействий,новый;
Перем СтарыйКонтрагент;
Перем СтараяДата;
Перем НачальнаяДатаДокумента;
//*****************************************************************************
// "служебные" функции и процедуры 

//*****************************************************************************
Функция ЗаголовокФормы() 
	Перем Заголовок, Название;

	Если ВидОперации =0  Тогда
		Заголовок = "Продажа реализатора ";
		Название ="Накладная №";
	Иначе
		Заголовок = "Продажа реализатора (возврат)";
		Название = "Накладная (возврат) №";
	КонецЕсли;
	
	Заголовок = Заголовок + глЗаголовок(Контекст);
	
	Форма.Заголовок(Заголовок);     
	Возврат Название;
КонецФункции //ЗаголовокФормы
                                                            
// ===============================
Процедура УстДоступность()
	Если Договор.Выбран()=1 Тогда
		Дост=0;
	Иначе
		Дост=1;
	КонецЕсли;  
	Если названиенабораправ()<>"Администратор" Тогда
	    Если распечатан=1 Тогда
	        форма.толькопросмотр(1);
	    КонецЕсли;
	КонецЕсли;	
	Форма.спВидТорговли.Доступность(Дост);
	Форма.тВидТорговли.Доступность(Дост);
КонецПроцедуры             

// ===============================
Процедура ИзмДоговор()
	Если ПустоеЗначение(Договор)=0 Тогда
		Если Договор.ВидТорговли<> Перечисление.ВидыТорговли.Консигнация Тогда
			глТрассировка("Можно выбирать только договор консигнации !!!",1);
			Договор=0;
		КонецЕсли;                              
	КонецЕсли;
КонецПроцедуры

//******************************************************************************
Процедура ОбновитьНадписи()
	Форма.ТекстВалюты.Заголовок(глСтрокаВалюты(Контекст));
	Форма.ТекстОснования.Заголовок(СокрП(Основание));
	Форма.ТекстПорядокОплаты.Заголовок(глСтрокаПорядокОплаты(Контекст));
КонецПроцедуры
                                       
// ===============================
Процедура ИзмВидТорговли()
	ВидТорговли = спВидТорговли.ПолучитьЗначение(спВидТорговли.ТекущаяСтрока());
КонецПроцедуры

// ===============================
Процедура ВыбранВидТорговли()
	спВидТорговли.ТекущаяСтрока(спВидТорговли.НайтиЗначение(ВидТорговли));
КонецПроцедуры
                       

//******************************************************************************
Процедура ИзмДатаДок()
	глПриИзмененииДатыДокумента(Контекст, СтараяДата);
КонецПроцедуры

//******************************************************************************
Функция Итого()
	// Показ итоговых сумм по документу
	Перем Результат,СуммаБезНДС, СуммаСНДС, СуммаНДС;
	СуммаБезНДС = Итог("СуммаБезНДС");
	СуммаСНДС = Итог("СуммаСНДС");
	СуммаНДС = СуммаСНДС - СуммаБезНДС;
	Результат = ?(СуммаБезНДС =0,"","Сумма="+ глФРМ(СуммаБезНДС, Валюта, 1))+
	            ?(СуммаНДС = 0,""," НДС=" + глФРМ(СуммаНДС, Валюта, 1))+ 
	            ?(СуммаСНДС = 0,""," Всего=" + глФРМ(СуммаСНДС , Валюта, 1));
	Возврат Результат;
КонецФункции

//*****************************************************************************
// функции и процедуры, вызываемые из формул элементов диалога

//******************************************************************************
Процедура Взаиморасчеты()
	глПоказатьДолг(Контекст, Контрагент, "Продажа");
КонецПроцедуры

// ===============================
Процедура Заполнить()
	УдалитьСтроки();
	Запрос=СоздатьОбъект("Запрос");
	ТекстЗапроса="";
	Если СравнитьТА()=-1  Тогда  // документ не новый, а существующий
		// так делаем, чтобы отделить новый Документ
		Если ДатаДок<>ПолучитьДатуТА() Тогда
			Дат=ТекущийДокумент();
			ТекстЗапроса=ТекстЗапроса+"
			|Период С Дат ПО Дат;";
		КонецЕсли;
	ИначеЕсли СравнитьТА()=-2  Тогда  // документ новый, и еще не записан
		Если ДатаДок<ПолучитьДатуТА() Тогда
			// если итоги не актуальны, то
			Дат=ДатаДок;
			ТекстЗапроса=ТекстЗапроса+"
			|Период С Дат ПО Дат;";
		КонецЕсли;
	КонецЕсли;
	
	ТекстЗапроса=ТекстЗапроса+"
	|ИзмТовар=Регистр.ПартииТоваров.Товар;
	|ИзмФирма=Регистр.ПартииТоваров.Фирма;
	|Статус=Регистр.ПартииТоваров.Статус;
	|ИзмКонтрагент=Регистр.ПартииТоваров.Контрагент;
	|ПрихДокумент=Регистр.ПартииТоваров.ПрихДокумент;
	|Остаток=Регистр.ПартииТоваров.ОстатокТовара;
	|Долг=Регистр.ПартииТоваров.ПродСтоимость;
	|Группировка ИзмТовар Без групп;
	|Функция КонДолг=КонОст(Долг);
	|Функция КонОстаток=КонОст(Остаток);
	|Условие(ИзмКонтрагент = Контрагент);
	|Условие((Статус=ОтданныйКупленный) ИЛИ (Статус=ОтданныйПринятый));
	|";
	Если ТипУчета>Упр Тогда
		ТекстЗапроса=ТекстЗапроса+"
		|Условие (ИзмФирма=Фирма);";
	Иначе
		ТекстЗапроса=ТекстЗапроса+"
		|Условие (ИзмФирма=глПустаяФирма);";
	КонецЕсли;
	Если ПустоеЗначение(ДокументОснование)=0 Тогда
		ТекстЗапроса=ТекстЗапроса+"
		|Условие (ПрихДокумент=ДокументОснование);";
	КонецЕсли;
	
	Если Запрос.Выполнить(ТекстЗапроса)=0 тогда
		Предупреждение("Запрос по партиям товаров, отданных на реализацию не выполнился!");
		Возврат;
	КонецЕсли;
	
	Пока Запрос.Группировка("ИзмТовар") = 1 Цикл
		Если Запрос.КонОстаток=0 тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока();
		Товар=Запрос.ИзмТовар;
		Если ТипУчета > Упр Тогда
			ВП = Гривня;
		Иначе
			ВП = Товар.ВалютаУчета;
		КонецЕсли;
		Количество=Запрос.КонОстаток;
		Единица=глВернутьБазовуюЕдиницуТовара(Товар);
		СуммаСНДС=глПересчет(Запрос.КонДолг,ВП,Дата_Курса,Валюта,Курс);
		ЦенаСНДС=СуммаСНДС/Запрос.КонОстаток;		
		Коэффициент=1;
		глВыч_суммы_накл(Контекст,1);
	КонецЦикла;
	
КонецПроцедуры


// ===============================
Процедура Подбор()
	Варианы= СоздатьОбъект("СписокЗначений");
	Варианы.ДобавитьЗначение("по каталогу");
	Варианы.ДобавитьЗначение("по прайсу");
	Если ВидОперации =0  Тогда
		Варианы.ДобавитьЗначение("по остаткам");
	КонецЕсли;	
	Результат = 0;
	Если Варианы.ВыбратьЗначение(,,Результат,,1)=1 Тогда
	    Если Результат = 1 Тогда
			глПодбор(Контекст,"номенклатура","ДляПодбора")
		ИначеЕсли Результат = 2 Тогда
			глПодбор(Контекст,"прайс_лист","ДляПодбора")
		ИначеЕсли Результат = 3 Тогда
			Заполнить();
	    КонецЕсли;
	КонецЕсли;
КонецПроцедуры

//*****************************************************************************
Процедура ВыборФирмы()
	// по кнопке редактирования параметров фирмы в докумнете
	Перем КонтекстДокумента;
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ИнформацияОфирме", КонтекстДокумента);
	ОбновитьНадписи();
КонецПроцедуры	
	
//*****************************************************************************
Процедура ВыборОплаты()
	// Процедура по кнопке редактирования параметров оплаты в докумнете
	Перем КонтекстДокумента;
	Перем СтараяКатегорияЦен;
	СтараяКатегорияЦен = КатегорияЦен;
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ИнформацияОценах", КонтекстДокумента);
	Если СтараяКатегорияЦен <> КатегорияЦен Тогда
	    // изменилась категория цен
		// перерисуем
		Если ПустоеЗначение(КонтекстПодбора) = 0 Тогда
		    КонтекстПодбора.Форма.Обновить();
		КонецЕсли;
	КонецЕсли;
	ОбновитьНадписи();
КонецПроцедуры	

//*****************************************************************************
Процедура ИзмКонтрагент() // Процедура при выборе Контрагента в докумнете
    Если Контрагент.Выбран()=1 Тогда                                      
		Если СтарыйКонтрагент <> Контрагент Тогда
			// изменили Контрагента
			// очистим договор
			Договор = ПолучитьПустоеЗначение("Документ.Договор");
			Если Константа.ПодставлятьОсновнойДоговор = Да Тогда
				// подставим договор по умолчанию                                 
				Если ПустоеЗначение(Контрагент.ОсновнойДоговорТорг) = 0 Тогда
					Если Фирма = Контрагент.ОсновнойДоговорТорг.Фирма Тогда
						Договор = Контрагент.ОсновнойДоговорТорг;
					КонецЕсли;	
				КонецЕсли;	
			КонецЕсли;
		    Если ПустоеЗначение(Контрагент.КатегорияЦен) = 0 Тогда
		    	КатегорияЦен = Контрагент.КатегорияЦен;
		    КонецЕсли;
		КонецЕсли;
	Иначе
		//Не выбран Контрагент - не имеет смысла показывать глубину и дату оплаты и договор
		Договор = ПолучитьПустоеЗначение("Документ.Договор");
	КонецЕсли;
	// Формирует и показывает дату оплаты
	СтарыйКонтрагент = Контрагент;
	УстДоступность();
	ОбновитьНадписи();
КонецПроцедуры	

//******************************************************************************
Процедура ПриВводеНаОсновании(ДокОснование)
	Перем ВидОсн;
	
	Если ПустоеЗначение(ДокОснование) = 0  Тогда
		ВидОсн = ДокОснование.Вид();		
		Валюта = ДокОснование.Валюта;
		Дата_Курса = ДокОснование.Дата_Курса;
		Курс = ДокОснование.Курс;   			                                               	
		глЗаполнитьШапкуНаОсн(Контекст,ДокОснование); 
	Иначе
		Заполнить();
		Возврат;
	КонецЕсли;	
	
	ВыбранВидТорговли();
	ИзмДоговор();
	
	Если ВидОсн <> "Договор" Тогда
		глСкопироватьТовСостав(Контекст,ДокОснование);  
		Если (Константа.НачальноеЗаполнение = Да) и (ВидОперации = 0) тогда	
			// проставляем сколько осталось товара,
			// который не был реализован
			// если указан документ-основание
			Если ПустоеЗначение(ДокументОснование) = 0 тогда
				// предпочтением является документ-основание
				ТипДО = "Документ."+ДокументОснование.Вид();
				НазначитьТип("Предпочтение",ТипДО);
			КонецЕсли;
			ВыбратьСтроки();
			Пока ПолучитьСтроку() = 1 цикл                   
				Если Товар.ВидТовара = Перечисление.ВидыТоваров.Услуга Тогда
					Продолжить;
				КонецЕсли;
				Фрм = ?(ТипУчета = Упр,глПустаяФирма,Фирма);
				КвоОстПр = Регистр.ПартииТоваров.СводныйОстаток(Фрм,Товар,ОтданныйПринятый,,,,ДокументОснование,"ОстатокТовара"); 
				КвоОстКуп = Регистр.ПартииТоваров.СводныйОстаток(Фрм,Товар,ОтданныйКупленный,,,,ДокументОснование,"ОстатокТовара"); 
				Если Количество = КвоОстПр/Коэффициент+КвоОстКуп/Коэффициент Тогда 
					Продолжить;
				Иначе
					Количество = КвоОстПр/Коэффициент+КвоОстКуп/Коэффициент;
				КонецЕсли;	
				глВыч_суммы_накл(Контекст);      
				Предпочтение = ДокументОснование.ТекущийДокумент();
			КонецЦикла;	
		ИначеЕсли ВидОперации = 1 тогда
			// проверяем сколько уже возвращено 	
			ВыбратьСтроки();
			Пока ПолучитьСтроку() = 1 цикл
				Если Товар.ВидТовара = Перечисление.ВидыТоваров.Услуга Тогда
					Продолжить;
				КонецЕсли;
				//КвоВозвр = Регистр.ВозвратыПартий.СводныйИтог(,,Товар,,,,,ДокументОснование,"ОстатокТовара"); 
				Фрм = ?(ТипУчета = Упр,глПустаяФирма,Фирма);
				КвоОст = Регистр.ВозвратыПартий.СводныйОстаток(ДокументОснование,Фрм,Товар,,,,,,"ОстатокТовара"); 
				Если КвоОст/Коэффициент = 0 Тогда
					Продолжить;
				Иначе	
					Количество = Количество + КвоОст/Коэффициент; 
				КонецЕсли;	
				глВыч_суммы_накл(Контекст);
			КонецЦикла;	
		КонецЕсли;	     
		// удаляем сторки с количеством 0
		ТаблЧ = СоздатьОбъект("ТаблицаЗначений");
		ВыгрузитьТабличнуюЧасть(ТаблЧ);
		КСтр = ТаблЧ.КоличествоСтрок();
		Пока КСтр <> 0 цикл
			ТаблЧ.ПолучитьСтрокуПоНомеру(КСтр);				
			Если (ТаблЧ.Количество = 0) И (ТаблЧ.Товар.ВидТовара <> Перечисление.ВидыТоваров.Услуга) Тогда
				ТаблЧ.УдалитьСтроку();
				КСтр = ТаблЧ.КоличествоСтрок();
			Иначе
				КСтр = КСтр-1;
			КонецЕсли;	  
		КонецЦикла;	
		ЗагрузитьТабличнуюЧасть(ТаблЧ);
		Если КоличествоСтрок() = 0 тогда
			глТрассировка("При начальном заполнении на основании "+ДокументОснование+", табличная часть пустая.",0);
		КонецЕсли;	
	Иначе
		Заполнить();
	КонецЕсли;
	
КонецПроцедуры
	
//*****************************************************************************
Процедура ВыборОснования()
	// Процедура по кнопке редактирования основания в докумнете
	Перем КонтекстДокумента; 
	СтарОснование = ДокументОснование;
	СтарДоговор = Договор;
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ОснованиеДокумента", КонтекстДокумента);
	Если ((ДокументОснование <> СтарОснование) и (ПустоеЗначение(ДокументОснование) = 0)) тогда
		ПриВводеНаОсновании(ДокументОснование);	
	ИначеЕсли Договор<>СтарДоговор тогда
		ПриВводеНаОсновании(Договор);			
	КонецЕсли;	
	ИзмДоговор();
	глУстДостСумм(Контекст);
	УстДоступность();
	ОбновитьНадписи();         	
КонецПроцедуры	

//******************************************************************************
Процедура Печать()
	Если (названиенабораправ()="Продавець") ИЛИ (названиенабораправ()="Менеджер")  ИЛИ (названиенабораправ()="МенеджерТоварищ")Тогда
		Если новый=1 Тогда
		    возврат;
		КонецЕсли;
	КонецЕсли;                       
	Если (названиенабораправ()="Продавець") ИЛИ (названиенабораправ()="Менеджер") ИЛИ (названиенабораправ()="МенеджерТоварищ") Тогда
		Распечатан=1; 		
		провести();			
	    //записать();
		Если проведен()=0 Тогда
		СТАТУСВОЗВРАТА(0);    
		КонецЕсли;
		форма.толькопросмотр(1);
		Если выбран()=1 Тогда
		    новый=0;
		КонецЕсли;		
	КонецЕсли;	
	Таб = СоздатьОбъект("Таблица");  
	
	ВП = Валюта;  
	КурсПечати = Курс;                     
	ДатаКурса = Дата_Курса;

	Если ПустоеЗначение(Контрагент) = 0 Тогда
		Если Контрагент.ВидКонтрагента <> Перечисление.ВидыКонтрагентов.Нерезидент Тогда
			Если ТипУчета > 0 Тогда
				ВП = Гривня;  
				КурсПечати = глКурсДляВалюты(ВП,ДатаДок);                     
				ДатаКурса = ДатаДок;
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	

	ПечФорма = "Накладная";
	
	Язык = глЯзык(ПечФорма); 	
	
	Таб.ИсходнаяТаблица(ПечФорма);
	
	глУстПропись(Валюта,Язык);
	
	Фирма.ИспользоватьДату(ДатаДок,1);
	Таб.ВывестиСекцию("Шапка");
	ВСоставе = 0;
	Ном = 1;
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл                
		Если Набор.Выбран()=1 Тогда
			Если ПустоеЗначение(ВСоставе)=1 Тогда
				Таб.ВывестиСекцию("ВСоставе");
				ВСоставе=Набор;
			ИначеЕсли ВСоставе<>Набор Тогда
				Таб.ВывестиСекцию("ВСоставе");
				ВСоставе=Набор;
			КонецЕсли;
		Иначе
			Если ПустоеЗначение(ВСоставе)=0 Тогда
				ВСоставе=0;
			КонецЕсли;
		КонецЕсли;          
		ПечЦена = глФРМ(глПересчет(ЦенаБезНДС,Валюта,Курс,ВП,ДатаДок),ВП,0);
		ПечСумма = глФРМ(глПересчет(СуммаБезНДС,Валюта,Курс,ВП,ДатаДок),ВП,0);
		Таб.ВывестиСекцию("Строка");
		Ном = Ном+1;
	КонецЦикла;
	ПечБезНДС = глФРМ(глПересчет(Итог("СуммаБезНДС"),Валюта,Курс,ВП,ДатаДок),ВП,0);
	ПечНДС = глФРМ(глПересчет(Итог("СуммаСНДС") - Итог("СуммаБезНДС"),Валюта,Курс,ВП,ДатаДок),ВП,0);
	ПечСНДС = глФРМ(глПересчет(Итог("СуммаСНДС"),Валюта,Курс,ВП,ДатаДок),ВП,0);	
	ПечНДСПропись = " "+Формат(глПересчет(Итог("СуммаСНДС") - Итог("СуммаБезНДС"),Валюта,Курс,ВП,ДатаДок),"Ч12.2")+" "+ВП.Кратко;
	ПечСНДСПропись = глСуммаПрописью(глПересчет(Итог("СуммаСНДС"),Валюта,Курс,ВП,ДатаДок),ВП);  
	Таб.ВывестиСекцию("Дно");
	Таб.ТолькоПросмотр(1);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.Опции(0,0,,);
	Таб.Показать("ПЕЧАТЬ: Продажа товаров реалиизатором","");
		Распечатан=1;
		записать();		
	
КонецПроцедуры

// ===============================
Процедура ЗаполнитьСпВидТорговли()
	
	спВидТорговли.ДобавитьЗначение(Перечисление.ВидыТорговли.Консигнация,Строка(Перечисление.ВидыТорговли.Консигнация));

КонецПроцедуры


//*****************************************************************************
// Предопределенные процедуры

//******************************************************************************
Процедура ВводНового(Скопирован) //Предопределенная процедура
    новый=1;   
	распечатан=0;
	глЗаполнитьШапку(Контекст);
	Если Скопирован=1 Тогда	//копирование документа
		Возврат;
	КонецЕсли;


	ДатаДок=РабочаяДата();
	Если ПустоеЗначение(глАктивныйДоговор) = 0 Тогда
		Контрагент = глАктивныйДоговор.Владелец;
		Договор = глАктивныйДоговор;
		КатегорияЦен = Контрагент.КатегорияЦен;
		СтарыйКонтрагент = Контрагент;
	Иначе
		Контрагент=Константа.ОсновнойПокупатель;
		ИзмКонтрагент();
	КонецЕсли;
	                                           
	Если Форма.Параметр="Возврат" Тогда                          
		ВидОперации = 1;
		Если ПустоеЗначение(Контрагент) = 1 Тогда
			Контрагент=Константа.ОсновнойПоставщик;
		КонецЕсли;
		Если ПустоеЗначение(Контрагент) = 0 Тогда
			ВремКатегорияЦен = Контрагент.КатегорияЦенПоставщика;
		КонецЕсли;
		Валюта=Константа.ОсновнаяВалютаЗакупки;
	Иначе
		ВидОперации = 0;
		Если ПустоеЗначение(Контрагент) = 1 Тогда
			Контрагент=Константа.ОсновнойПокупатель;
		КонецЕсли;
		Если ПустоеЗначение(Контрагент) = 0 Тогда
			КатегорияЦен = Контрагент.КатегорияЦен;
		КонецЕсли;
		Валюта=Константа.ОсновнаяВалютаПродажи;
	КонецЕсли;
	
	Дата_Курса = ДатаДок;
	Если ПустоеЗначение(Валюта) = 1 Тогда
		Валюта = Гривня;
	КонецЕсли;
	Курс=глКурсДляВалюты(Валюта,Дата_Курса);

	Если ПустоеЗначение(КатегорияЦен)=1 Тогда
		КатегорияЦен = Автор.КатегорияЦен;
	КонецЕсли;
	
	ВидТорговли = Перечисление.ВидыТорговли.Консигнация; 
	ВыбранВидТорговли();
	
КонецПроцедуры

//******************************************************************************
Процедура ВводНаОсновании(ДокОснование) //Предопределенная процедура
    Перем ВидДокОсн;
    новый=1;   
	распечатан=0;
	
	ВидДокОсн = ДокОснование.Вид();

	Если (ВидДокОсн="ПродажаРеализатора") Тогда
		ВидОперации = 1;
	КонецЕсли;

	Автор = глПользователь;
	ДатаДок=РабочаяДата();

	ЗасчитыватьОплатуПоСлужДоговору = 0;
	
	ПриВводеНаОсновании(ДокОснование);  

	Если ПустоеЗначение(КатегорияЦен)=1 Тогда
		Если ПустоеЗначение(Контрагент)=0 Тогда
			КатегорияЦен = Контрагент.КатегорияЦен;    
		КонецЕсли;	    
	КонецЕсли;          
	
	Если ПустоеЗначение(КатегорияЦен)=1 Тогда
		КатегорияЦен = Автор.КатегорияЦен;
	КонецЕсли;            
	
КонецПроцедуры          

Процедура ПриНачалеРедактированияСтроки()
	Если ПустоеЗначение(Предпочтение)=1 Тогда
		Форма.Предпочтение.НазначитьТип("Справочник.Контрагенты");
		Предпочтение=ПустоеЗначение("Справочник.Контрагенты");
	КонецЕсли;
КонецПроцедуры

Процедура ПриРедактированииНовойСтроки()
	Форма.Предпочтение.НазначитьТип("Справочник.Контрагенты");
	Предпочтение=ПустоеЗначение("Справочник.Контрагенты");
КонецПроцедуры

//*****************************************************************************
Процедура ПриОткрытии() //Предопределенная процедура
	НачальнаяДатаДокумента = ДатаДок;
	глПроверкаРазрешенияРедактирования(Контекст);
	глУстановкаРеквизитаТип(Контекст);
	Если новый<>1 тогда
		новый=0;
	КонецЕсли;  	
	// Если открыли только на просмотр, то надо кнопки сделать недоступными
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кнОК.Доступность(0);
		Форма.кнПровести.Доступность(0);
		Форма.кнДействия.Доступность(0);
		
		Форма.кнПодбор.Доступность(0);
		
		Форма.кнОснование.Доступность(0);
	Иначе
		Форма.КнопкаПоУмолчанию("кнОК");
	КонецЕсли;  

	Если (названиенабораправ()="Продавець") ИЛИ (названиенабораправ()="Менеджер")  ИЛИ (названиенабораправ()="МенеджерТоварищ")Тогда   
		Если распечатан=1 Тогда
		    форма.кнподбор.доступность(0);			
		КонецЕсли;						
		ЕСли (проведен()=1) ИЛИ (ПометкаУдаления()=1) тогда		
			форма.толькопросмотр(1);			
		КонецЕсли;		
		форма.распечатан.доступность(0);
	Иначе
		форма.распечатан.доступность(1);
	КонецЕсли;

	глУстВидимостьЦен(Контекст);
	глУстДостСумм(Контекст);     
	
    ОбновитьНадписи();        
		
	УстДоступность();
	СтараяДата = ДатаДок;
	
	Если ПустоеЗначение(ВидТорговли)=1 Тогда
	    ВидТорговли = Перечисление.ВидыТорговли.Консигнация;
		ВыбранВидТорговли();
	КонецЕсли;	
КонецПроцедуры //При открытии

// ===============================
Процедура ВыборПредпочтения()
	спОтбор = СоздатьОбъект("СписокЗначений");
	
	спСтатусы=СоздатьОбъект("СписокЗначений");
	спСтатусы.ДобавитьЗначение(ОтданныйКупленный);
	спСтатусы.ДобавитьЗначение(ОтданныйПринятый);
	спОтбор.Установить("Статус",спСтатусы);
	
	спОтбор.Установить("Статус",спСтатусы);
	спОтбор.Установить("Контрагент",Контрагент);
	спОтбор.Установить("Контекст",глВзятьКонтекст(Контекст));  
	
	ОткрытьФормуМодально("Справочник.Контрагенты.ВыборПредпочтения", спОтбор);
КонецПроцедуры


//*****************************************************************************
Процедура ПриНачалеВыбораЗначения(Рекв,Флаг)
	Флаг=0;
	Если Рекв="Фирма" Тогда
		ВыборФирмы();
	ИначеЕсли Рекв="КатегорияЦен" Тогда
	    ВыборОплаты();
	ИначеЕсли Рекв="Предпочтение" Тогда
	    ВыборПредпочтения();
	Иначе
		Флаг=1;
	КонецЕсли;
КонецПроцедуры
//*****************************************************************************
Процедура ОбработкаПодбора(Выб) //Предопределенная процедура
	глПриОбработкеПодбора(Выб,Контекст);
КонецПроцедуры //Обработка подбора

//*****************************************************************************
Процедура ПриЗаписи() //Предопределенная процедура
	Если глМожноЗаписатьДокумент(Контекст)=0 Тогда
		СтатусВозврата(0);                        
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли; 
	новый=0;	
	ГруппаПользователей = Автор.Родитель;
КонецПроцедуры  

// ===============================
Процедура ПриЗакрытии()
КонецПроцедуры	
                                             
Форма.Товар.ВыполнятьФормулуТолькоПриИзменении(1);

//*****************************************************************************
//Инициализирум список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
    |ТоварныйСостав,
	|ОбновлениеЦен,
	|СтруктураПодчиненности,
	|ДвиженияДокумента,
	|ВводНаОсновании,
	|ОткрытьВЖурнале,
	|Подчиненные,
	|ИзмКомментария");
    
ЗаполнитьСпВидТорговли();
ВыбранВидТорговли();
