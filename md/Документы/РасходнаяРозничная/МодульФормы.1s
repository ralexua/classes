// ===============================
// Описание переменных 
Перем СписокДействий;  // Список действий по документу
Перем СтарыйКонтрагент;
Перем КатегорияЦен;
Перем СтараяДата;

Перем ВариантыЗаполнения;
Перем НачальнаяДатаДокумента; // Для контроля даты документа
Перем ОстаткиТоваров, РезервыТоваров, ОбщРег;
// ===============================
// "служебные" функции и процедуры 
                                       

// ===============================
Процедура Взаиморасчеты()
	глПоказатьДолг(Контекст, Контрагент, "Продажа");
КонецПроцедуры

// ===============================
Процедура ОбновитьНадписи()
	Форма.ТекстВалюты.Заголовок(глСтрокаВалюты(Контекст));
	Форма.ТекстОснования.Заголовок(СокрП(Основание));          
	Форма.ТекстПорядокОплаты.Заголовок(глСтрокаПорядокОплаты(Контекст)); 
КонецПроцедуры

// ===============================
Функция ЗаголовокФормы() 
	Перем Заголовок, Название;
	
	Если ВидОперации = 0 Тогда
		Заголовок = "Расходная розничная";
		Название ="Накладная №";
	Иначе
		Заголовок = "Расходная розничная (возврат)";
		Название ="Накладная (возврат) №";
	КонецЕсли;
    
	Заголовок = Заголовок + глЗаголовок(Контекст);
	
	Форма.Заголовок(Заголовок);     
	Возврат Название;
КонецФункции //ЗаголовокФормы

Процедура Заполнить() Далее
// ===============================
Процедура Подбор()
	Варианы= СоздатьОбъект("СписокЗначений");
	Варианы.ДобавитьЗначение("по остаткам");
	Варианы.ДобавитьЗначение("по каталогу");
	Варианы.ДобавитьЗначение("по прайсу");
	Варианы.ДобавитьЗначение("по штрих-коду");
	Результат = 0;
	Если Варианы.ВыбратьЗначение(,,Результат,,1)=1 Тогда
		Если Результат = 1 Тогда
			Заполнить();
	    ИначеЕсли Результат = 2 Тогда
			глПодбор(Контекст,"номенклатура","ДляПодбора")
		ИначеЕсли Результат = 3 Тогда
			глПодбор(Контекст,"прайс_лист","ДляПодбора")
		ИначеЕсли Результат = 4 Тогда
			ОткрытьПодбор("Обработка.ПодборПоШтрихКоду");
	    КонецЕсли;
	КонецЕсли;	
КонецПроцедуры
 
// ===============================
Процедура УстДоступностьКнопок()
	Если Форма.ТолькоПросмотр()=1 Тогда
		Форма.кнОК.Доступность(0);
		Если РежимПроведения = 0 Тогда
			Форма.кнПровести.Доступность(0);
		КонецЕсли;
		Форма.кнДействия.Доступность(0);
		
		Форма.кнПредварительно.Доступность(0);
		
		Форма.кнЗаполнить.Доступность(0);
		
		Форма.кнОснование.Доступность(0);
	Иначе
		Форма.КнопкаПоУмолчанию("кнОК");
	КонецЕсли;	
КонецПроцедуры

// ===============================
Процедура ПровестиДокумент()
	Если глМожноЗаписатьДокумент(Контекст)=0 Тогда
		Возврат;
	КонецЕсли;
	Записать();
	Провести();
	Если Проведен() = 1 Тогда
		глПроверкаРазрешенияРедактирования(Контекст);
		УстДоступностьКнопок();
	КонецЕсли;
КонецПроцедуры                      

// ===============================
Процедура ЧастичноПровести()
	фМожноПроводить = 1;
	Если Проведен()=1 Тогда
		Если РежимПроведения=0 Тогда
			фМожноПроводить = 0;
			Предупреждение("Документ "+ПредставлениеВида()+" № "+СокрЛП(НомерДок)+" от "+Формат(ДатаДок,"ДДММГГГГ")+
			               " полностью проведен. Предварительное проведение невозможно.");
		КонецЕсли;
	ИначеЕсли ДатаДок<ПолучитьДатуТА() Тогда
		фМожноПроводить = 0;
		Предупреждение("Предварительное проведение задним числом невозможно.");
	ИначеЕсли ДатаДок<>РабочаяДата() Тогда
		фМожноПроводить = 0;
		Предупреждение("Дата документа "+ПредставлениеВида()+" № "+СокрЛП(НомерДок)+" от "+Формат(ДатаДок,"ДДММГГГГ")+
		               " отличается от рабочей даты ("+Формат(РабочаяДата(),"ДДММГГГГ")+"). Предварительное проведение невозможно.");
	КонецЕсли;
				
	Если фМожноПроводить = 1 Тогда
		Провести(3,1);
		ЗаголовокФормы();
		Если Проведен() = 1 Тогда
			глПроверкаРазрешенияРедактирования(Контекст);
			УстДоступностьКнопок();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура УстДоступность(Закладка="")
	
	Если ВидТорговли = Перечисление.ВидыТорговли.Нал Тогда
		ДостНал = 1;
	Иначе
		ДостНал = 0;
	КонецЕсли;
	
	Форма.ЭККА.Видимость(ДостНал);
	Форма.тЭККА.Видимость(ДостНал);
	Форма.Касса.Видимость(ДостНал);
	Форма.тКассы.Видимость(ДостНал);
	Если Закладка<>"Доверенность" Тогда
		Форма.Получено.Видимость(ДостНал);
		Форма.тПолучено.Видимость(ДостНал);
		Форма.Сдача.Видимость(ДостНал);
		Форма.тСдача.Видимость(ДостНал);
	КонецЕсли;
	Форма.тЧекПробит.Видимость(ДостНал);
	
КонецПроцедуры
                       
// ===============================
Процедура ИзмВидТорговли()
	ВидТорговли = спВидТорговли.ПолучитьЗначение(спВидТорговли.ТекущаяСтрока());
	УстДоступность();
КонецПроцедуры

// ===============================
Процедура ЗаполнитьСпВидТорговли()
	спВидТорговли.ДобавитьЗначение(Перечисление.ВидыТорговли.Нал,Строка(Перечисление.ВидыТорговли.Нал));
	спВидТорговли.ДобавитьЗначение(Перечисление.ВидыТорговли.Предоплата,Строка(Перечисление.ВидыТорговли.Предоплата));
КонецПроцедуры

// ===============================
Процедура ВыбранВидТорговли()   
	спВидТорговли.ТекущаяСтрока(спВидТорговли.НайтиЗначение(ВидТорговли));
КонецПроцедуры

// ===============================
Процедура ВыборДаты()
	глПриИзмененииДатыДокумента(Контекст, СтараяДата);
	ОбновитьНадписи();
КонецПроцедуры           

// ===============================
Процедура ВыборФирмы()
	// по кнопке редактирования параметров фирмы в докумнете
	Перем КонтекстДокумента;
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ИнформацияОфирме", КонтекстДокумента);
КонецПроцедуры	
	
// ===============================
Процедура ВыборОплаты()
	// Процедура по кнопке редактирования параметров оплаты в докумнете
	Перем КонтекстДокумента;
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ОткрытьФормуМодально("Обработка.ИнформацияОценах", КонтекстДокумента);
	Форма.ТекстВалюты.Заголовок(глСтрокаВалюты(Контекст));
КонецПроцедуры	

// ===============================
Процедура ИзмКонтрагент()	// Процедура при выборе Контрагента в докумнете
    Если Контрагент.Выбран()=1 Тогда                                      
		Если СтарыйКонтрагент <> Контрагент Тогда
			// изменили Контрагента
			// очистим договор
			Договор = ПолучитьПустоеЗначение("Документ.Договор");
			Если Константа.ПодставлятьОсновнойДоговор = Да Тогда
				// подставим договор по умолчанию                                 
				Если ПустоеЗначение(Контрагент.ОсновнойДоговорТорг) = 0 Тогда
					Если Фирма = Контрагент.ОсновнойДоговорТорг.Фирма Тогда
						Договор = Контрагент.ОсновнойДоговорТорг;
					КонецЕсли;	
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;
	Иначе
		//Не выбран Контрагент - не имеет смысла показывать глубину и дату оплаты и договор
		Договор = ПолучитьПустоеЗначение("Документ.Договор");
	КонецЕсли;
	// Формирует и показывает дату оплаты
	СтарыйКонтрагент = Контрагент;
	Форма.ТекстВалюты.Заголовок(глСтрокаВалюты(Контекст));
КонецПроцедуры	

	
// ===============================
Процедура ЗаполнениеШапкиНаОсновании(ДокОснование)
	Если ПустоеЗначение(ДокОснование)=1 Тогда
		Возврат;
	КонецЕсли;
	Если ПустоеЗначение(Валюта) = 1 Тогда
		Валюта=ДокОснование.Валюта;
		Дата_Курса=ДокОснование.Дата_Курса;
		Курс=ДокОснование.Курс;
	КонецЕсли;
	глЗаполнитьШапкуНаОсн(Контекст,ДокОснование);
КонецПроцедуры

// ===============================**
Процедура Заполнить()
	Если ПустоеЗначение(ДокументОснование)=1 Тогда
	    Возврат;
	КонецЕсли;
	
	Если ДокументОснование.Вид()= "Счет" Тогда
		Если ДокументОснование.Проведен()=1 Тогда
			// Если вводим на основании Счета, то спецификацию записываем
			// исходя из остатков зарезервированных товаров
			ВремРегистры=СоздатьОбъект("Регистры");
			Рег1=ВремРегистры.РезервыТоваров;
			// документ новый
			Если ПолучитьДатуТА()>ДатаДок Тогда
				// если итоги не актуальны, то остатки берем из временногно расчета Регистра
				Рег1.ВременныйРасчет();
				ВремРегистры.РассчитатьРегистрыНа(ДатаДок+1);
			КонецЕсли;
			// Нужно вычислить скидку 
			СуммаСкидки = 0;
			СписокТоваров=СоздатьОбъект("СписокЗначений");
			ДокументОснование.ВыбратьСтроки();
			Пока ДокументОснование.ПолучитьСтроку()=1 Цикл
				Если ДокументОснование.Товар.Выбран()=0 Тогда
					Продолжить;
				КонецЕсли;
				СуммаСкидки = СуммаСкидки + ДокументОснование.СуммаСкидки * (100 + глПроцентНДС(ДокументОснование.Товар.СтавкаНДС.Получить(ДокументОснование.ДатаДок)))/100;
				Если ДокументОснование.Товар.ВидТовара=Перечисление.ВидыТоваров.Услуга Тогда
					// услуги
					НоваяСтрока();
					Товар=ДокументОснование.Товар;
					ЦенаСНДС=ДокументОснование.ЦенаСНДС;
					Количество=ДокументОснование.Количество;
					Единица=ДокументОснование.Единица;
					Коэффициент=ДокументОснование.Коэффициент;
					СуммаСНДС=ДокументОснование.СуммаБезСкидки;
					Продолжить;
				Иначе
					Если СписокТоваров.НайтиЗначение(ДокументОснование.Товар)=0 Тогда
						// не вносим строку с товаром, если такую уже внесли
						// таким образом отсекаем возможное
						// дублирование строк по товару в Счете
						РезервПоСчету=Рег1.Остаток(ДокументОснование.Товар,ДокументОснование,"РезервТовара");
						Если РезервПоСчету>0 Тогда
							НоваяСтрока();
							Товар=ДокументОснование.Товар;
							ЦенаСНДС=ДокументОснование.ЦенаСНДС;
							Коэффициент=?(ДокументОснование.Коэффициент=0,1,ДокументОснование.Коэффициент);
							Количество=РезервПоСчету/Коэффициент;
							Единица=ДокументОснование.Единица;
							Коэффициент=Коэффициент;
							глВыч_суммы_накл(Контекст);
							Набор=ДокументОснование.Набор;
							СписокТоваров.ДобавитьЗначение(Товар);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Рег1=0;
		КонецЕсли;
	ИначеЕсли ДокументОснование.Вид()= "РасходнаяРозничная" Тогда
		глСкопироватьТовСостав(Контекст,ДокументОснование);
	КонецЕсли;
КонецПроцедуры

// ===============================
Процедура ВыборОснования()
	// Процедура по кнопке редактирования основания в докумнете
	КонтекстДокумента = глВзятьКонтекст(Контекст);
	ДокОснование=ДокументОснование;
	ОткрытьФормуМодально("Обработка.ОснованиеДокумента", КонтекстДокумента);
	Если (ДокОснование<>ДокументОснование) И (ПустоеЗначение(ДокументОснование)=0) Тогда
		УдалитьСтроки();
		ЗаполнениеШапкиНаОсновании(ДокументОснование);
		Заполнить();
	КонецЕсли;
	ОбновитьНадписи();
	глУстДостСумм(Контекст);
КонецПроцедуры	

// ===============================
Процедура ВыборПредпочтения()
	
	спОтбор = СоздатьОбъект("СписокЗначений");
	
	спСтатусы=СоздатьОбъект("СписокЗначений");
	спСтатусы.ДобавитьЗначение(РозницаКупленный);
	спСтатусы.ДобавитьЗначение(РозницаПринятый);
	спСтатусы.ДобавитьЗначение(РозницаПринятыйБезПраваПередачи);	
	спОтбор.Установить("Статус",спСтатусы);
	
	спОтбор.Установить("Контекст",глВзятьКонтекст(Контекст));  
	
	ОткрытьФормуМодально("Справочник.Контрагенты.ВыборПредпочтения", спОтбор);
КонецПроцедуры

// ===============================
Процедура ВыборКассы()
	спКассы = глЗаполнитьСпКассы(Фирма,Валюта);
	спКассы.ВыбратьЗначение(Касса,,,,2);
КонецПроцедуры

// ===============================
Процедура ВыборЭККА()
	спЭККА = глЗаполнитьСпЭККА("Чек");
	спЭККА.ВыбратьЗначение(ЭККА,,,,2);
КонецПроцедуры

// ===============================
Процедура ПриНачалеВыбораЗначения(Рекв,Флаг)
	Флаг=0;
	Если (Рекв="Фирма") или (Рекв="Склад") Тогда
		ВыборФирмы();
	ИначеЕсли Рекв="КатегорияЦен" Тогда
	    ВыборОплаты();
	ИначеЕсли Рекв="Предпочтение" Тогда
	    ВыборПредпочтения(); 
	ИначеЕсли Рекв="Касса" Тогда
		ВыборКассы();		
	ИначеЕсли Рекв="ЭККА" Тогда
		ВыборЭККА();
	Иначе
		Флаг=1;
	КонецЕсли;
КонецПроцедуры
                        
// ===============================
// функции и процедуры, вызываемые из формул элементов диалога

// ===============================
Процедура Печать()
	
	Таб = СоздатьОбъект("Таблица");    

	ВП = Валюта;  
	КурсПечати = Курс;                     
	ДатаКурса = Дата_Курса;

	Если ПустоеЗначение(Контрагент) = 0 Тогда
		Если Контрагент.ВидКонтрагента <> Перечисление.ВидыКонтрагентов.Нерезидент Тогда
			Если ТипУчета > 0 Тогда
				ВП = Гривня;  
				КурсПечати = глКурсДляВалюты(ВП,ДатаДок);                     
				ДатаКурса = ДатаДок;
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	

	ПечФорма = "Накладная";
	Язык = глЯзык(ПечФорма); 	
	Таб.ИсходнаяТаблица(ПечФорма);
	глУстПропись(Валюта,Язык);
	
	Фирма.ИспользоватьДату(ДатаДок,1);
	Таб.ВывестиСекцию("Шапка");
	
	Ном = 1;
	ВСоставе = 0;
	СуммаНДС = 0;
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл                
		Если Набор.Выбран()=1 Тогда
			Если ПустоеЗначение(ВСоставе)=1 Тогда
				Таб.ВывестиСекцию("ВСоставе");
				ВСоставе=Набор;
			ИначеЕсли ВСоставе<>Набор Тогда
				Таб.ВывестиСекцию("ВСоставе");
				ВСоставе=Набор;
			КонецЕсли;
		Иначе
			Если ПустоеЗначение(ВСоставе)=0 Тогда
				ВСоставе=0;
			КонецЕсли;
		КонецЕсли;         
		ПечЦена = глФРМ3(глПересчет(ЦенаСНДС,Валюта,Курс,ВП,ДатаДок),ВП,0);
		ПечСумма = глФРМ(глПересчет(СуммаСНДС,Валюта,Курс,ВП,ДатаДок),ВП,0);
		Таб.ВывестиСекцию("Строка");
		Ном = Ном+1;
		ПроцНДС = глПроцентНДС(Товар.СтавкаНДС.Получить(ДатаДок));
		СуммаНДС = СуммаНДС + Окр(СуммаСНДС * (ПроцНДС/(100 + ПроцНДС)),2);
	КонецЦикла;
	Если СуммаСкидки<>0 Тогда
		ПечСкидка = глФРМ(глПересчет(СуммаСкидки,Валюта,Курс,ВП,ДатаДок),ВП,0);
		Таб.ВывестиСекцию("Скидка");
	КонецЕсли;
	ПечНДС = глФРМ(глПересчет(СуммаНДС,Валюта,Курс,ВП,ДатаДок),ВП,0);
	ПечСНДС = глФРМ(глПересчет(Итог("СуммаСНДС")-СуммаСкидки,Валюта,Курс,ВП,ДатаДок),ВП,0);	
	ПечНДСПропись = " "+Формат(глПересчет(СуммаНДС,Валюта,Курс,ВП,ДатаДок),"Ч12.2")+" "+ВП.Кратко;
	ПечСНДСПропись = глСуммаПрописью(глПересчет(Итог("СуммаСНДС")-СуммаСкидки,Валюта,Курс,ВП,ДатаДок),ВП);  
	Таб.ВывестиСекцию("Дно");
	Таб.ТолькоПросмотр(1);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.Опции(0,0,,);
	Таб.Показать("ПЕЧАТЬ: Расходная накладная","");
	
КонецПроцедуры

// ===============================
Процедура Сдача()
	ВыданаСдача=Получено-(Итог("СуммаСНДС")-СуммаСкидки);
	ВыданаСдача=?(ВыданаСдача<0,0,ВыданаСдача);
КонецПроцедуры

// ===============================
// Предопределенные процедуры

Процедура ПриВыбореЗакладки(Номер,Значение)
	Форма.ИспользоватьСлой("Совместный,"+Значение,2);
	УстДоступность(Значение);                
КонецПроцедуры

// ===============================
Процедура ВводНового(Скопирован)
	
	Автор=глПользователь;
	
	глЗаполнитьШапку(Контекст);
	
	Если Скопирован=1 Тогда	//копирование документа
		ЧекПробит=0;
		Возврат;
	КонецЕсли;

	спЭККА = глЗаполнитьСпЭККА("Чек");
    
	Если спЭККА.РазмерСписка() > 1 тогда
		Стр = "";
		ЭККА = спЭККА.ПолучитьЗначение(1,Стр);	
		Если ЭККА = ПолучитьПустоеЗначение("Справочник.ЭККА") тогда
			ЭККА = спЭККА.ПолучитьЗначение(2,Стр);	
		КонецЕсли;	
	ИначеЕсли спЭККА.РазмерСписка() = 1 тогда
		ЭККА = спЭККА.ПолучитьЗначение(1,Стр);	
	КонецЕсли;

	Если ЭККА.Выбран()=1 Тогда
		Если (ЭККА.РежимРаботы = Перечисление.РежимыРаботыЭККА.Регистрация) или
			(ЭККА.РежимРаботы = Перечисление.РежимыРаботыЭККА.Автономный) Тогда
			Склад = ЭККА.Магазин;    
		КонецЕсли;
	КонецЕсли;
	
	Если ПустоеЗначение(Склад)=1 Тогда
		Склад = Константа.ОсновнойСклад;    
	КонецЕсли;
                                         
	Если Форма.Параметр="Возврат" Тогда                          
		ВидОперации = 1;
	Иначе
		ВидОперации = 0;
	КонецЕсли;
	
	ДатаДок=РабочаяДата();
	Валюта=Константа.ОсновнаяВалютаПродажи;
	Дата_Курса=ДатаДок;
	Если ПустоеЗначение(Валюта) = 1 Тогда
		Валюта = Гривня;
	КонецЕсли;
	
	Курс=глКурсДляВалюты(Валюта,Дата_Курса);

	ЧекПробит=0;

	Если ПустоеЗначение(глАктивныйДоговор) = 0 Тогда
		Контрагент = глАктивныйДоговор.Владелец;
		Договор = глАктивныйДоговор;
		СтарыйКонтрагент = Контрагент;
	Иначе
		Контрагент=Константа.ОсновнойПокупатель;
	КонецЕсли;
	
	ИзмКонтрагент(); 	
	
	Если ПустоеЗначение(ВидТорговли)=1 Тогда
	    ВидТорговли = Перечисление.ВидыТорговли.Нал;
	КонецЕсли;
	
	УстДоступность();
	
	Если ВидТорговли = Перечисление.ВидыТорговли.Нал Тогда
		Касса = глНайтиКассу(Фирма,Валюта);
	КонецЕсли;
	
	ВыбранВидТорговли();
	
КонецПроцедуры

// ===============================
Процедура ВводНаОсновании(ДокОснование)

	СинонимДокумента	= ПредставлениеВида();
	СинонимОснования	= ДокОснование.ПредставлениеВида();

	Список = глПолучитьСписокВводимыхНаОсновании(ДокОснование);
	Поз = Список.НайтиЗначение(Вид());
	
	Если Поз=0 Тогда
		СтатусВозврата(0);
		Предупреждение("Документ """+СинонимДокумента+""" нельзя вводить на основании """+СинонимОснования+"""");
		Возврат;
	КонецЕсли;
        
	Автор=глПользователь;
	Если ДокОснование.Вид()="Счет" Тогда
		Склад = Константа.ОсновнойСклад;
	ИначеЕсли ДокОснование.Вид()="РасходнаяРозничная" Тогда
		ВидОперации = 1;
		ЭККА = ДокОснование.ЭККА;
		Получено = ДокОснование.Получено;
		ВыданаСдача = ДокОснование.ВыданаСдача;
	КонецЕсли;
	
	ЗаполнениеШапкиНаОсновании(ДокОснование);
	Заполнить();
	
	ВыбранВидТорговли();
	
	УстДоступность();	
	ЧекПробит=0; 
	
	// проверяем сколько уже возвращено 	
	РегВозвратыПартий = СоздатьОбъект("Регистр.ВозвратыПартий");
	Если ВидОперации = 1 тогда
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 цикл
			КвоВозвр = Регистр.ВозвратыПартий.СводныйИтог(,,Товар,,,,,ДокументОснование,"ОстатокТовара"); 
			Количество = Количество + КвоВозвр; 
		КонецЦикла;	
	КонецЕсли;	     
	
КонецПроцедуры

// ===============================
Процедура ПриОткрытии()

	НачальнаяДатаДокумента = ДатаДок;

	глПроверкаРазрешенияРедактирования(Контекст);
	
	Если ПустоеЗначение(Константа.РозничнаяКатегорияЦен) = 0 Тогда
		КатегорияЦен = Константа.РозничнаяКатегорияЦен.Наименование + ",";
	Иначе
		КатегорияЦен = "";
	КонецЕсли;
	                      
	УстДоступностьКнопок();

	Если Проведен()=1 Тогда
	    Форма.кнПредварительно.Видимость(0);
	КонецЕсли;
	
	СтараяДата = ДатаДок;
	ОбновитьНадписи();
	                            
	//Если документ еще не проведен, тогда 
	//Предварительное проведение делаем только в потоке
	Если ( Проведен() = 0 ) Тогда
		ПроводитьПослеТА(1,1);
	КонецЕсли;                  
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Основной","Основные");
	Форма.Закладки.ДобавитьЗначение("Доверенность","Доверенность");
	Форма.ИспользоватьСлой("Основной, Совместный",2); 
	
	УстДоступность();  
	
	глУстДостСумм(Контекст);
	
	Если ПустоеЗначение(ВидТорговли)=1 Тогда
	    ВидТорговли = Перечисление.ВидыТорговли.Нал;
		ВыбранВидТорговли();
	КонецЕсли;		
КонецПроцедуры

// ===============================
Процедура ПриВводеСтроки()
	Сдача();
КонецПроцедуры
                                 
Процедура ПриНачалеРедактированияСтроки()
	Сдача();
	Если ПустоеЗначение(Предпочтение)=1 Тогда
		Форма.Предпочтение.НазначитьТип("Справочник.Контрагенты");
		Предпочтение=ПустоеЗначение("Справочник.Контрагенты");
	КонецЕсли;
КонецПроцедуры

Процедура ПриРедактированииНовойСтроки()
	Сдача();
	Форма.Предпочтение.НазначитьТип("Справочник.Контрагенты");
	Предпочтение=ПустоеЗначение("Справочник.Контрагенты");
КонецПроцедуры

// ===============================
Процедура ОбработкаПодбора(Выб)  
	глПриОбработкеПодбора(Выб,Контекст);
КонецПроцедуры

// ===============================
Процедура ОбработкаВнешнегоСобытия(Источник,Событие,Данные)
	// Процедура разбирает штрих-код, считанный сканером
	// и заполняет строки накладной
	Перем Упаковка,ТекКоличество;

	Если РаботаСоСканеромШтрихКода<>2 Тогда
		Возврат;
	КонецЕсли;
	
	ТекКоличество=0;
	Упаковка=0;
	Если Событие="BarCodeValue"	Тогда
		СканерШтрихКода.ПосылкаДанных = 1;
        Если Форма.ТолькоПросмотр() = 1 Тогда
        	// форма открыта только для просмотра
			Возврат;
        КонецЕсли;
		// Определение типа штрих-кода
		Если ШтрихКодИмеетПрефикс=1 Тогда
			Если Найти("MPJS",Лев(Данные,1))<>0 Тогда
				// Штрих-код определяет товар
				Данные=Сред(Данные,2,СтрДлина(Данные)-1);
			Иначе
				Сообщить("Формат считанного штрих-кода не предназначен для товаров");
			КонецЕсли;
		КонецЕсли;
		Тов=СоздатьОбъект("Справочник.Номенклатура");
		Упак=СоздатьОбъект("Справочник.Единицы");

		Лев2 = Лев(Данные,2);
		//Если Лев2 = "22" Тогда
		//	// Используется свой внутренний штрих-код товара
		//	// Штрих-код построен по коду товара и весу
		//	Если Тов.НайтиПоКоду(Сред(Данные,3,5))=0 Тогда
		//		Сообщить("Товар с кодом "+Сред(Данные,3,5)+" не найден");
		//		Возврат;
		//	КонецЕсли;
		//	ТекКоличество=Число(Сред(Данные,8,5))/1000;
		//	Упаковка = Тов.ЕдиницаПоУмолчанию;
		//Иначе
		// Используется основной штрих-код товара
		// или свой внутренний штрих-код товара с префиксом "20,21"
		Если Упак.НайтиПоРеквизиту("ШтрихКод",Данные,1)=0 Тогда
			Сообщить("Товар со штрих кодом "+Данные+" не найден");
			Возврат;
		КонецЕсли;
		Упаковка=Упак.ТекущийЭлемент();
		Тов=Упаковка.Владелец;
		//КонецЕсли;
		
		// добавим товар
		ВыбратьСтроки();
		ФлагПоиска = 0;
		Пока ПолучитьСтроку() > 0  Цикл
			Если Товар=Тов.ТекущийЭлемент() Тогда
				ФлагПоиска=1;
				Прервать;
			КонецЕсли;
		КонецЦикла;

		Если ФлагПоиска=0 Тогда
			// товар вносим первый раз
			НоваяСтрока();
			Товар=Тов.ТекущийЭлемент();
			ЦенаТовара = глВернутьЦену(Товар, Константа.РозничнаяКатегорияЦен);
			Если ПустоеЗначение(ЦенаТовара) = 1 Тогда
				// у товара отсутствует цена
				Сообщить("У товара "+Товар.Наименование+" отсутствует "+Константа.РозничнаяКатегорияЦен+" цена");
				Возврат;
			КонецЕсли;
			ЦенаТовара.ИспользоватьДату(ДатаДок);
			ВалютаЦены=ЦенаТовара.Валюта;
			ВремЦена = ЦенаТовара.Цена;
			Единица=Упаковка;
			Если Упаковка <> ЦенаТовара.Единица Тогда
				// есть цена за другую единицу измерения. Нужно пересчитать цену
				ВремЦена = ВремЦена * Упаковка.Коэффициент / ЦенаТовара.Единица.Коэффициент;
			КонецЕсли;
			ВремЦена = глПересчет(ВремЦена,ВалютаЦены,Дата_курса,Валюта,Курс);
			// цены нужно округлить
			Если Валюта = Гривня Тогда
				ВремЦена=Окр(ВремЦена,Константа.КоэффОкрГрнЦены,1);    
			КонецЕсли;
			ЦенаСНДС = ВремЦена;
			Количество=?(ПустоеЗначение(ТекКоличество)=1,1,ТекКоличество);
			Коэффициент=Упаковка.Коэффициент;
			глВыч_суммы_накл(Контекст,1);
		Иначе
			// товар уже есть в документе. Увеличиваем количество
			ПромКоличество=?(ПустоеЗначение(ТекКоличество)=1,1,ТекКоличество);
			Если ПустоеЗначение(Упаковка)=0 Тогда
				ПромКоэффициент=Упаковка.Коэффициент;
			Иначе
				ПромКоэффициент=1;
			КонецЕсли;
			Количество=(Единица.Коэффициент*Количество+ПромКоличество*ПромКоэффициент);
			глВыч_суммы_накл(Контекст);
		КонецЕсли;
		
	КонецЕсли;
	АктивизироватьСтроку();
КонецПроцедуры

// ===============================
Процедура ПриЗаписи()
	Если глМожноЗаписатьДокумент(Контекст)=0 Тогда
		СтатусВозврата(0);                        
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры
                  
// ===============================
Процедура ПриЗакрытии()
КонецПроцедуры	
                      
Форма.Товар.ВыполнятьФормулуТолькоПриИзменении(1);

// ===============================
// При входе в Форму запомним промежуточные переменные

// ===============================
//Инициализирум список действий по кнопке "Действия"
СписокДействий = глПолучитьСписокДействий("
    |ТоварныйСостав,
	|ОбновлениеЦен,
	|СтруктураПодчиненности,
	|ДвиженияДокумента,
	|ВводНаОсновании,
	|ОткрытьВЖурнале,
	|Подчиненные,
	|ИзмКомментария");

ЗаполнитьСпВидТорговли();
ВыбранВидТорговли();